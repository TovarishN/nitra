using Nitra;
using Nitra.Declarations;
using Nitra.Serialization2;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Imperative;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Ast
{
  public sealed class TupleRuleTypeScope : RuleTypeScope
  {
    _type    : RuleTupleTypeSymbol;
    _context : DependentPropertyEvalContext;

    public this(type : RuleTupleTypeSymbol, context : DependentPropertyEvalContext)
    {
      _type    = type;
      _context = context;
    }

    protected override CreateMembers() : BindResult[DeclarationSymbol]
    {
      unless (_type.IsElemTypesEvaluated)
        return BindResult.Nil();

      mutable result = LightList();
      mutable index  = 0;
      foreach (t in _type.ElemTypes)
      {
        ++index;
        match (t.FirstDeclarationOrDefault)
        {
          | null with (loc = Location.Default)
          | d    with (loc = d.Location) =>
            def symbol = ExternalDeclaration.[RuleFieldSymbol](loc, "Item" + index).DefineSymbol();
            symbol.Type = t;
            symbol.EvalProperties(_context);
            result.Add(symbol);
        }
      }

      result.ToBindResult()
    }

    public override Serialize(writer : IO.BinaryWriter, metadataWriter : MetadataWriter) : void
    {
      metadataWriter.WriteObject(_type, writer);
    }

    public static Deserialize(reader : IO.BinaryReader, metadataReader : MetadataReader) : this
    {
      def type = metadataReader.ReadObject(reader);
      TupleRuleTypeScope(type, metadataReader.Project.Data)
    }
  }
}
