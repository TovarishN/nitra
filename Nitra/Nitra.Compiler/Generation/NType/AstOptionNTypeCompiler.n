using Nitra.Model;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;

namespace Nitra.Compiler
{
  public sealed class AstOptionNTypeCompiler: NTypeCompiler
  {
    public this(compiler : NTypeCompiler)
    {
      this._compiler = compiler;
    }

    private _compiler : NTypeCompiler;

    public override Environment : EmitterEnvironment { get { _compiler.Environment } }

    public override CompileProjectedPropertyType(propertySymbol : ProjectedPropertySymbol) : FixedType
    {
      _compiler.CompileProjectedPropertyType(propertySymbol)
    }

    public override CompileStructuralPropertyType(propertySymbol : StructuralPropertySymbol) : AstPropertyType
    {
      _compiler.CompileStructuralPropertyType(propertySymbol)
    }

    [Memoize]
    public override CompileDependentPropertyType(propertySymbol : DependentPropertySymbol) : FixedType
    {
      | DependentPropertySymbol(Direction = Out, LinkedProperty = None) =>
        def itemType = _compiler.CompileDependentPropertyType(propertySymbol);
        Environment.Manager.CoreEnv.BindFixedType(<[ ValueOption[$(itemType : typed)] ]>)

      | _ => _compiler.CompileDependentPropertyType(propertySymbol);
    }

    public override Compile(t : NType) : PExpr
    {
      _compiler.Compile(t)
    }
  }
}
