using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Compiler
{
  [Record]
  public sealed class BitField
  {
    _index     : int;
    _fieldName : string;
    _mask      : int;

    public Index : int { get { _index } }

    public Read() : PExpr
    {
      <[ unchecked(this.$(_fieldName : usesite) %&& $(_mask : int)) ]>
    }

    public Set() : PExpr
    {
      <[ unchecked(this.$(_fieldName : usesite) = this.$(_fieldName : usesite) %| $(_mask : int)) ]>
    }

    public Clear() : PExpr
    {
      <[ unchecked(this.$(_fieldName : usesite) = this.$(_fieldName : usesite) %& $(~_mask : int)) ]>
    }

    public static CheckAll(bits : Seq[BitField]) : PExpr
    {
      mutable acc : PExpr;
      foreach (g in bits.GroupBy(bit => bit._fieldName))
      {
        mutable mask;
        foreach (bit in g)
          mask |= bit._mask;

        PExprUtils.CombineAnd(ref acc, <[ unchecked(this.$(g.Key : usesite) & $(mask : int) == $(mask : int)) ]>);
      }
      acc ?? <[ false ]>
    }
  }
}
