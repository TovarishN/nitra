using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Declarations
{
  public abstract class TypeUnifier
  {
    public static Instance : TypeUnifier { get; protected set; }

    protected _typeVarInitContext : DependentPropertyEvalContext = DependentPropertyEvalContext(null);

    protected abstract IsSubtypeUnwraped(subtype : TypeSymbol, supertype : TypeSymbol) : bool;

    public virtual CreateFreeTypeVar() : TypeVarSymbol
    {
      CreateTypeVar(null, null)
    }

    public virtual CreateTypeVar(baseTypeConstraint : TypeSymbol, derivedTypeConstraint : TypeSymbol) : TypeVarSymbol
    {
      def var = BoundedTypeVarSymbol();

      when (baseTypeConstraint : object != null)
        var.BaseTypeConstraint = baseTypeConstraint;

      when (derivedTypeConstraint : object != null)
        var.DerivedTypeConstraint = derivedTypeConstraint;

      var.EvalProperties(_typeVarInitContext);
      var
    }

    protected virtual UnifyUnwraped(typeSymbol1 : TypeSymbol, typeSymbol2 : TypeSymbol, updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      match (typeSymbol1, typeSymbol2)
      {
        | (TypeVarSymbol as v, TypeVarSymbol(IsBaseTypeConstraintEvaluated = false, IsDerivedTypeConstraintEvaluated = false) as unboundVar)
        | (TypeVarSymbol(IsBaseTypeConstraintEvaluated = false, IsDerivedTypeConstraintEvaluated = false) as unboundVar, TypeVarSymbol as v) =>
          BindTypeVar(unboundVar, v, ref updatedVariables);
          true

        | (TypeVarSymbol as v1, TypeVarSymbol as v2) =>
          mutable newBaseTypeConstraint;
          match (v1.IsBaseTypeConstraintEvaluated, v2.IsBaseTypeConstraintEvaluated)
          {
            | (false, false) => ()
            | (true,  false) => newBaseTypeConstraint = v1.BaseTypeConstraint;
            | (false,  true) => newBaseTypeConstraint = v2.BaseTypeConstraint;
            | (true,   true) =>
              def c1 = v1.BaseTypeConstraint;
              def c2 = v2.BaseTypeConstraint;
              if (c1.Equals(c2) || TryUnify(c1, c2))
                newBaseTypeConstraint = c1
              else if (IsSubtype(c1, c2))
                newBaseTypeConstraint = c1
              else if (IsSubtype(c2, c1))
                newBaseTypeConstraint = c2
              else
                return false; // base type constraints are not compatible
          }

          mutable newDerivedTypeConstraint;
          match (v1.IsDerivedTypeConstraintEvaluated, v2.IsDerivedTypeConstraintEvaluated)
          {
            | (false, false) => ()
            | (true,  false) => newDerivedTypeConstraint = v1.DerivedTypeConstraint;
            | (false,  true) => newDerivedTypeConstraint = v2.DerivedTypeConstraint;
            | (true,   true) =>
              def c1 = v1.DerivedTypeConstraint;
              def c2 = v2.DerivedTypeConstraint;
              if (c1.Equals(c2) || TryUnify(c1, c2))
                newDerivedTypeConstraint = c1
              else if (IsSubtype(c1, c2))
                newDerivedTypeConstraint = c2
              else if (IsSubtype(c2, c1))
                newDerivedTypeConstraint = c1
              else
                return false; // derived type constraints are not compatible
          }

          mutable type;
          when (newBaseTypeConstraint : object != null && newDerivedTypeConstraint : object != null)
          {
            if (newBaseTypeConstraint.Equals(newDerivedTypeConstraint) || TryUnify(newBaseTypeConstraint, newDerivedTypeConstraint))
              type = newBaseTypeConstraint;
            else unless (IsSubtype(newDerivedTypeConstraint, newBaseTypeConstraint))
              return false; // merged constraints are not consistent
          }

          when (type : object == null)
            type = CreateTypeVar(newBaseTypeConstraint, newDerivedTypeConstraint);

          BindTypeVar(v1, type, ref updatedVariables);
          BindTypeVar(v2, type, ref updatedVariables);
          true

        | (type, var is TypeVarSymbol)
        | (var is TypeVarSymbol, type) =>
          BindTypeVar(var, type, ref updatedVariables);

          when (var.IsBaseTypeConstraintEvaluated)
          {
            def baseTypeConstraint = var.BaseTypeConstraint;
            unless (type.Equals(baseTypeConstraint) || IsSubtype(type, baseTypeConstraint) || UnifyImpl(type, baseTypeConstraint, ref updatedVariables))
              return false; // base type constraint not satisfied
          }

          when (var.IsDerivedTypeConstraintEvaluated)
          {
            def derivedTypeConstraint = var.DerivedTypeConstraint;
            unless (type.Equals(derivedTypeConstraint) || IsSubtype(derivedTypeConstraint, type) || UnifyImpl(type, derivedTypeConstraint, ref updatedVariables))
              return false; // derived type constraint not satisfied
          }

          true

        | _ =>
          mutable ti1 = typeSymbol1;
          mutable args1;
          mutable ti2 = typeSymbol2;
          mutable args2;

          when (typeSymbol1.DecomposeType() is VSome(s))
          {
            ti1   = s.TypeInfo;
            args1 = s.TypeArguments;
          }

          when (typeSymbol2.DecomposeType() is VSome(s))
          {
            ti2   = s.TypeInfo;
            args2 = s.TypeArguments;
          }

          if (ti1 : object == ti2)
            UnifyAll(ref args1, ref args2, ref updatedVariables);
          else
            false
      }
    }

    public IsSubtype(subtype : TypeSymbol, supertype : TypeSymbol) : bool
    {
      def unwrappedSubtype   = Unwrap(subtype);
      def unwrappedSupertype = Unwrap(supertype);
      when (unwrappedSubtype : object == unwrappedSupertype)
        return true;

      IsSubtypeUnwraped(unwrappedSubtype, unwrappedSupertype)
    }

    public TryProvide(t : TypeSymbol, derivedTypeConstraint : TypeSymbol) : bool
    {
      match (Unwrap(derivedTypeConstraint))
      {
        | TypeVarSymbol as tv
        | c with tv = CreateTypeVar(null, c) => TryUnify(t, tv)
      }
    }

    public Provide(t : TypeSymbol, derivedTypeConstraint : TypeSymbol) : bool
    {
      match (Unwrap(derivedTypeConstraint))
      {
        | TypeVarSymbol as tv
        | c with tv = CreateTypeVar(null, c) => Unify(t, tv)
      }
    }

    protected ProvideImpl(t : TypeSymbol, derivedTypeConstraint : TypeSymbol, updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      match (Unwrap(derivedTypeConstraint))
      {
        | TypeVarSymbol as tv
        | c with tv = CreateTypeVar(null, c) => UnifyImpl(t, tv, ref updatedVariables)
      }
    }

    public TryRequire(t : TypeSymbol, baseTypeConstraint : TypeSymbol) : bool
    {
      match (Unwrap(baseTypeConstraint))
      {
        | TypeVarSymbol as tv
        | c with tv = CreateTypeVar(c, null) => TryUnify(t, tv)
      }
    }

    public Require(t : TypeSymbol, baseTypeConstraint : TypeSymbol) : bool
    {
      match (Unwrap(baseTypeConstraint))
      {
        | TypeVarSymbol as tv
        | c with tv = CreateTypeVar(c, null) => Unify(t, tv)
      }
    }

    protected RequireImpl(t : TypeSymbol, baseTypeConstraint : TypeSymbol, updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      match (Unwrap(baseTypeConstraint))
      {
        | TypeVarSymbol as tv
        | c with tv = CreateTypeVar(c, null) => UnifyImpl(t, tv, ref updatedVariables)
      }
    }

    public TryUnify(t1 : TypeSymbol, t2 : TypeSymbol) : bool
    {
      mutable updatedVariables;
      def result = UnifyImpl(t1, t2, ref updatedVariables);
      ResetTypeVars(ref updatedVariables);
      result
    }

    public Unify(t1 : TypeSymbol, t2 : TypeSymbol) : bool
    {
      mutable updatedVariables;
      def result = UnifyImpl(t1, t2, ref updatedVariables);
      unless (result)
        ResetTypeVars(ref updatedVariables);
      result
    }

    protected UnifyImpl(typeSymbol1 : TypeSymbol, typeSymbol2 : TypeSymbol, updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      def t1 = Unwrap(typeSymbol1);
      def t2 = Unwrap(typeSymbol2);
      when (t1 : object == t2)
        return true;

      UnifyUnwraped(t1, t2, ref updatedVariables)
    }

    protected UnifyAll(ts1 : ref LightList[TypeSymbol], ts2 : ref LightList[TypeSymbol], updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      when (ts1.Count != ts2.Count)
        return false;

      for (mutable i = 0; i < ts1.Count; ++i)
        unless (UnifyImpl(ts1[i], ts2[i], ref updatedVariables))
          return false;

      true
    }

    protected UnifyAll(ts1 : IList[TypeSymbol], ts2 : IList[TypeSymbol], updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      when (ts1.Count != ts2.Count)
        return false;

      for (mutable i = 0; i < ts1.Count; ++i)
        unless (UnifyImpl(ts1[i], ts2[i], ref updatedVariables))
          return false;

      true
    }

    protected RequireAll(ts1 : IList[TypeSymbol], ts2 : IList[TypeSymbol], updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      when (ts1.Count != ts2.Count)
        return false;

      for (mutable i = 0; i < ts1.Count; ++i)
        unless (RequireImpl(ts1[i], ts2[i], ref updatedVariables))
          return false;

      true
    }

    protected ProvideAll(ts1 : IList[TypeSymbol], ts2 : IList[TypeSymbol], updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      when (ts1.Count != ts2.Count)
        return false;

      for (mutable i = 0; i < ts1.Count; ++i)
        unless (ProvideImpl(ts1[i], ts2[i], ref updatedVariables))
          return false;

      true
    }

    protected virtual Unwrap(t : TypeSymbol) : TypeSymbol
    {
      | tv is TypeVarSymbol when tv.IsTypeEvaluated => Unwrap(tv.Type)
      | _ => t
    }

    protected static BindTypeVar(var : TypeVarSymbol, type : TypeSymbol, updatedVariables : ref LightList[TypeVarSymbol]) : void
    {
      var.Type = type;
      updatedVariables.Add(var);
    }

    protected ResetTypeVars(vars : ref LightList[TypeVarSymbol]) : void
    {
      def e = vars.GetEnumerator();
      while (e.MoveNext())
      {
        def var                   = e.Current;
        def baseTypeConstraint    = if (var.IsBaseTypeConstraintEvaluated)    var.BaseTypeConstraint    else null;
        def derivedTypeConstraint = if (var.IsDerivedTypeConstraintEvaluated) var.DerivedTypeConstraint else null;

        var.ResetProperties();

        when (baseTypeConstraint : object != null)
          var.BaseTypeConstraint = baseTypeConstraint;

        when (derivedTypeConstraint : object != null)
          var.DerivedTypeConstraint = derivedTypeConstraint;

        var.EvalProperties(_typeVarInitContext);
      }
    }
  }
}
