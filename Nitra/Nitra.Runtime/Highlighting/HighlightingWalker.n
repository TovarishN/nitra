using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Internal;
using Nitra.Internal.Recovery;
using Nitra.Runtime.Reflection;

using System;
using System.Collections.Generic;
using System.Linq;

type SpanInfoSet = System.Collections.Generic.HashSet[Nitra.SpanInfo];

namespace Nitra.Runtime.Highlighting
{

  public class HighlightingWalker : WalkerBase[SpanInfoSet]
  {
    range : NSpan;

    public this(range : NSpan)
    {
      //HashSet;
      this.range = range;
    }

    public OnToken(spanClass : SpanClass, startPos : int, endPos : int, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      _ = parseResult;
      when (endPos >= 0)
      {
        when (spanClass != null && range.IntersectsWith(startPos, endPos) && endPos > startPos)
          _ = context.Add(SpanInfo(NSpan(startPos, endPos), spanClass));
      }
    }

    public override OnRegularCall(ruleInfo : SubruleInfo.RegularCall, startPos : int, endPos : int, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      OnToken(ruleInfo.SpanClass, startPos, endPos, parseResult, context);
    }

    public override OnTokenString(ruleInfo : SubruleInfo.TokenString, startPos : int, endPos : int, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      OnToken(ruleInfo.SpanClass, startPos, endPos, parseResult, context);
    }

    public override OnSimpleCall(ruleInfo : SubruleInfo.SimpleCall, startPos : int, endPos : int, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      when (range.IntersectsWith(startPos, endPos))
        base.OnSimpleCall(ruleInfo, startPos, endPos, parseResult, context);
    }

    public override OnExtensibleCall(ruleInfo : SubruleInfo.ExtensibleCall, startPos : int, endPos : int, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      when (range.IntersectsWith(startPos, endPos))
        base.OnExtensibleCall(ruleInfo, startPos, endPos, parseResult, context);
    }

    public override OnOption(ruleInfo : SubruleInfo.Option, startPos : int, endPos : int, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      when (range.IntersectsWith(startPos, endPos))
        base.OnOption(ruleInfo, startPos, endPos, parseResult, context);
    }

    public override OnList(ruleInfo : SubruleInfo.List, startPos : int, endPos : int, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      when (range.IntersectsWith(startPos, endPos))
        base.OnList(ruleInfo, startPos, endPos, parseResult, context);
    }

    public override OnListWithSeparator(ruleInfo : SubruleInfo.ListWithSeparator, startPos : int, endPos : int, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      when (range.IntersectsWith(startPos, endPos))
        base.OnListWithSeparator(ruleInfo, startPos, endPos, parseResult, context);
    }

    public override WalkSimple(startPos : int, endPos : int, ruleParser : SimpleRuleParser, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      base.WalkSimple(startPos, endPos, ruleParser, parseResult, context);

      def spanClass = ruleParser.Descriptor.SpanClass;
      when (spanClass != null && range.IntersectsWith(startPos, endPos) && endPos > startPos)
        _ = context.Add(SpanInfo(NSpan(startPos, endPos), spanClass));
    }

    public override WalkPrefix(rawTreePtr : int, startPos : int, endPos : int, ruleParser : PrefixRuleParser, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      base.WalkPrefix(rawTreePtr, startPos, endPos, ruleParser, parseResult, context);
      when (endPos >= 0)
      {
        def spanClass = ruleParser.Descriptor.SpanClass;
        when (spanClass != null && range.IntersectsWith(startPos, endPos) && endPos > startPos)
          _ = context.Add(SpanInfo(NSpan(startPos, endPos), spanClass));
      }
    }

    public override WalkPostfix(rawTreePtr : int, startPos : int, endPos : int, ruleParser : PostfixRuleParser, parseResult : IParseResult, context : SpanInfoSet) : void
    {
      base.WalkPostfix(rawTreePtr, startPos, endPos, ruleParser, parseResult, context);
      when (endPos >= 0)
      {
        def spanClass = ruleParser.Descriptor.SpanClass;
        when (spanClass != null && range.IntersectsWith(startPos, endPos) && endPos > startPos)
          _ = context.Add(SpanInfo(NSpan(startPos, endPos), spanClass));
      }
    }
  }
}
