using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Declarations;

using System;
using System.Collections.Generic;
using System.Collections.Immutable;
using System.Linq;

namespace Nitra.ClientServer.Server
{
  using SpanInfo2 = Nitra.ClientServer.Messages.SpanInfo;
  using NSpan2    = Nitra.ClientServer.Messages.NSpan;

  [Record]
  internal class CollectSymbolsAstVisitor : IAstVisitor
  {
    _spanClassesManager : SpanClassesManager;
    _completeWordPosOpt : int;
    [RecordIgnore] _spanInfos : ImmutableArray[SpanInfo2].Builder = ImmutableArray.CreateBuilder();
    [RecordIgnore] _refs      : HashSet[IRef] = HashSet();

    public GetSpanInfos() : ImmutableArray[SpanInfo2]
    {
      def comparer = SpanInfoComparer(_spanClassesManager);
      _spanInfos.Sort(comparer);
      _spanInfos.ToImmutable()
    }

    public Visit(parseTree : IAst) : void
    {
      parseTree.Accept(this);
    }

    public Visit(name : Name) : void implements IAstVisitor.Visit
    {
      def span = name.Span;

      when (!name.IsSymbolEvaluated)
        return;

      def sym       = name.Symbol;
      def spanClass = sym.SpanClass;

      when (spanClass == Nitra.Language.DefaultSpanClass)
        return;

      unless (span.IntersectsWith(_completeWordPosOpt))
        Add(span, spanClass);
    }

    public Visit(reference : Reference) : void implements IAstVisitor.Visit
    {
      when (reference?.IsRefEvaluated)
        Visit(reference.Ref)
    }

    public Visit(r : IRef) : void
    {
      when (r == null || !_refs.Add(r))
        return;

      when (r.ResolvedTo != null)
      {
        Visit(r.ResolvedTo);
        return;
      }

      def span      = r.Location.Span;

      if (r.IsAmbiguous && r.IgnoreAmbiguities)
      {
        foreach (symbol in r.GetSymbols())
        {
          def spanClass = symbol.SpanClass;
          when (spanClass == Nitra.Language.DefaultSpanClass || span.IntersectsWith(_completeWordPosOpt))
            continue;
          Add(span, spanClass);
        }
      }
      else
      {
        def spanClass = r.SpanClass;

        when (spanClass != Nitra.Language.DefaultSpanClass && !span.IntersectsWith(_completeWordPosOpt))
          Add(span, spanClass);
      }
    }

    Add(span : NSpan, spanClass : SpanClass) : void
    {
      when (spanClass == Nitra.Language.ErrorSpanClass)
      {
        assert(true);
      }
      _spanInfos.Add(SpanInfo2(NSpan2(span.StartPos, span.EndPos), _spanClassesManager[spanClass]));
    }
  } // class
} // namespace
