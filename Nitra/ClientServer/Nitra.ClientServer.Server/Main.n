using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ClientServer.Messages;
using Nitra.ClientServer.Server;
using Nitra.ClientServer.Server.Lsp;
using Nitra.Logging;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.Globalization;
using System.IO;
using System.IO.Pipes;
using System.Linq;
using System.Threading;

module Program
{
  Main(args : array[string]) : void
  {
    //assert2(false, "Start debugger");
    def isLsp = args.Length == 1 && array["--lsp", "-lsp", "/lsp"].Contains(args[0], StringComparer.OrdinalIgnoreCase);

    Log.Init("Nitra.ClientServer.Server", !isLsp);
    Log.Message("Process ID: " + Process.GetCurrentProcess().Id);
#if !NOSERVERWINDOW
    //assert2(false, "Start debugger");
#endif

    when (args.Length != 1)
    {
      assert2(false, "Invalid number of parameters!");
      Log.Message("Invalid number of parameters, expected: <pipe name> or -lsp");
      Environment.Exit(42);
    }

    Nitra.NitraUtils.IdeMode = true;

    using (adapter = if (isLsp) LspProtocolAdapter() else NitraProtocolAdapter(args[0]))
    {
      Log.Message("Attempting to connect to pipes...");
      adapter.Connect();

      def router = Router(adapter);

      Log.Message("Connected to pipes.");

      def readerThread = Thread(fun()
      {
        foreach (message in adapter.Receive())
        {
          router.AddMessage(message);
          when (message is ClientMessage.Shutdown)
            break;
        }
      });
      readerThread.Name = "Incomming messages reader";
      readerThread.CurrentCulture = CultureInfo.InvariantCulture;
      readerThread.IsBackground = true;
      readerThread.Start();
      Log.Message("Server started.");
      router.Wait();
      Log.Message("Server stopped.");
      Log.Flush();
    }
  }
}
