using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.ClientServer.Messages;

using System;
using System.Collections.Concurrent;
using System.Console;
using System.Collections.Generic;
using System.Collections.Immutable;
using System.Diagnostics;
using System.Globalization;
using System.IO;
using System.IO.Pipes;
using System.Linq;
using System.Reflection;
using System.Threading;

namespace Nitra.ClientServer.Client
{
  using Nitra.ClientServer.Messages.AsyncServerMessage;

  public sealed class NitraClient : IDisposable
  {
    public  StringManager    : StringManager { get; }
    public  ResponseMap      : ConcurrentDictionary[int, Action[AsyncServerMessage]] = ConcurrentDictionary();
            MainPipeName     : string                                                = Guid.NewGuid().ToString();
            _locker          : object = object();
            _recorder        : Recorder;
    mutable _mainPipeClient  : NamedPipeClientStream;
    mutable _responsePipe    : NamedPipeClientStream;
    mutable _serverProc      : Process;
    mutable _mainPipeWriter  : BinaryWriter;
    mutable _isDisposed      : bool;
    mutable _mainPipeReader  : BinaryReader;

    volatile mutable _isRestarting : bool;

    public this([NotNull] stringManager : StringManager)
    {
      StringManager = stringManager;
      _recorder     = Recorder(this);
      Start();
    }

    Restart(): void
    {
      lock (_locker)
      {
        _isRestarting = true;
        try
        {
          Start();
          _recorder.Play();
        }
        finally
          _isRestarting = false;
      }
    }
    
    Start() : void
    {
      def executingAssemblyPath = Assembly.GetExecutingAssembly().Location;
      def assembliesPath        = Path.GetDirectoryName(executingAssemblyPath);
      def serverPath            = Path.Combine(assembliesPath, "Nitra.ClientServer.Server.exe");

      def startInfo = ProcessStartInfo() <- {
        FileName    = serverPath;
        Arguments   = MainPipeName;
#if NOSERVERWINDOW
        UseShellExecute = false;
        CreateNoWindow = true;
#else
        WindowStyle = ProcessWindowStyle.Minimized;
#endif
      };

      _serverProc = Process.Start(startInfo);

      assert2(_serverProc != null);
      Log("Server started.");
      _mainPipeClient           = NamedPipeClientStream(".", MainPipeName + Constants.RequestPipeSuffix, PipeDirection.Out);
      _mainPipeWriter           = BinaryWriter(_mainPipeClient);
      _responsePipe             = NamedPipeClientStream(".", MainPipeName + Constants.ResponsePipeSuffix, PipeDirection.In);
      _mainPipeReader           = BinaryReader(_responsePipe);
      _mainPipeClient.Connect();
      _responsePipe.Connect();
      Log("Connected to server started.");

      def readerThread          = Thread(AsyncResponseReaderThreadMain);
      readerThread.Name         = "AsyncResponseReader";
      readerThread.IsBackground = true;
      readerThread.Start();
    }

    AsyncResponseReaderThreadMain() : void
    {
      def asyncResponsePipet  = NamedPipeClientStream(".", MainPipeName + Constants.AsyncResponsePipeSuffix, PipeDirection.In);
      def asyncResponseReader = BinaryReader(asyncResponsePipet);
      asyncResponsePipet.Connect();

      while (!_isDisposed && asyncResponsePipet.IsConnected)
      {
        try
        {
          def msg = Deserializer.Deserialize.[AsyncServerMessage](asyncResponseReader);
          Trace.WriteLine(msg);

          mutable handler;
          if (ResponseMap.TryGetValue(msg.FileId, out handler))
          {
            handler(msg);
          }
          else if (ResponseMap.TryGetValue(-1, out handler))
          {
            handler(msg);
          }
          else
          {
            //assert2(false);
          }
        }
        catch
        {
          | _ is EndOfStreamException =>
            Trace.WriteLine("AsyncResponseReader disconnected.");
            unless (_isDisposed)
            {
              // Server was failed! Restart it.
              Restart();
            }
            
            return;

          | e => Trace.WriteLine("AsyncResponseReader exception: " + e);
        }
      }
    }

    public Send([NotNull] msg : ClientMessage) : void
    {
      lock (_locker)
      {
        assert2(!_isRestarting);
        SendInternal(msg);
        _recorder.Record(msg);
      }
    }

    internal SendInternal([NotNull] msg : ClientMessage) : void
    {
      when(_isDisposed)
        return;
      
      when (!_mainPipeClient.IsConnected)
      {
        assert2(true);
      }
      msg.Serialize(_mainPipeWriter);
    }
    
    public Receive[T]() : T
      where T: ServerMessage
    {
      ReceiveServerMessage() :> T
    }

    ReceiveServerMessage() : ServerMessage
    {
      Deserializer.Deserialize.[ServerMessage](_mainPipeReader)
    }
    
    public Flush() : void
    {
      _mainPipeWriter.Flush();
    }

    public Dispose() : void
    {
      when (_isDisposed)
        return;

      Send(Messages.ClientMessage.Shutdown());

      _mainPipeClient.Dispose();
      _responsePipe.Dispose();
      _isDisposed = true;
      GC.SuppressFinalize(this);
    }

    protected override Finalize() : void
    {
      Dispose();
    }

    Log(msg : string, params args: array[object]) : void
    {
      WriteLine(msg, args);
    }
  }
}
