using Nitra;
using Nitra.Declarations;
using DotNet.AstUtils;

namespace DotNet
{
  /// The AssemblyNamespace declare namespace from a single assembly or a single project.
  abstract declaration AssemblyNamespace : Namespace
  {
  }

  abstract declaration Namespace : NamespaceMemberDeclaration, Container
  {
    sealed symbol
    {
      Kind      = "namespace";
      SpanClass = DotNetLang.NamespaceSpanClass;
      Scope     = MemberTable;
      IsSameIdentity = AstUtils.IsSameIdentity(0, candidate);
      CanMerge       = candidate is Namespace || candidate is ExternalDeclaration[NamespaceSymbol];
      //MakeHint       = "<keyword>namespace</keyword> " + AstUtils.Combine(DeclaredIn, this);
      DeclaredInOpt = if (DeclaredIn == null) VNone() else VSome(DeclaredIn);
    }
  }

  declaration ImplicitNamespace : AssemblyNamespace
  {
    inout DeclaredInPipe : NamespaceSymbol;

    DeclaredIn                      = DeclaredInPipeIn;
    Symbol.DeclaredIn               = DeclaredIn;
    ContainingTable                 = DeclaredInPipeIn.MemberTable;
    DeclaredInPipeOut               = Symbol;
  }

  declaration ExplicitNamespace : AssemblyNamespace, NamespaceBody
  {
    EnteredScope                    = MakeEnteredScope(context, Scope, DeclaredIn, Symbol);
    Path.DeclaredInPipeIn           = DeclaredIn;
    ContainingTable                 = Path.DeclaredInPipeOut.MemberTable;
    Members.DeclaredIn              = Symbol;
    Symbol.DeclaredIn               = Path.DeclaredInPipeOut;

    Path            : ImplicitNamespace*;
  }

  /// The CompositeNamespace declaration combined namespaces from all loaded assemblies.
  declaration CompositeNamespace : Namespace
  {
  }

  abstract ast NamespaceBody : BindableAst
  {
  stage 1:
    out EnteredScope : Scope;

    Members.OpeningIn         = EmptyScope.Instance;
    Members.UsingAliasesScope = TableScope("aliases");
    Members.CurrentScope      = this.Scope;
    Members.Scope             = MakeMembersScope(EnteredScope, Members.OpeningOut, Members.UsingAliasesScope);

    Members         : NamespaceMember*;
  }
}
