using Nitra.Declarations;

using DotNet;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace DotNet
{
  public class TypeUnifier : Nitra.Declarations.TypeUnifier
  {
    public static InitInstance() : void
    {
      Instance = TypeUnifier();
    }

    protected override Unwrap(t : TypeSymbol) : TypeSymbol
    {
      | alias is TypeAliasSymbol when alias.IsReplacementEvaluated =>
        def replacement = alias.Replacement;
        when (replacement.IsSymbolEvaluated)
          when (replacement.Symbol is TypeSymbol as type)
            return Unwrap(type);

        base.Unwrap(t)

      | _ => base.Unwrap(t)
    }

    protected override IsSubtypeUnwraped(subtype : TypeSymbol, supertype : TypeSymbol) : bool
    {
      when (subtype.Equals(supertype))
        return true;
      // TODO: оптимизировать рабту с object
      //when (supertype : object == context.GetVoidType())
      //  return true;

      when (subtype is SupportsInheritanceTypeSymbol(IsBaseTypeSetEvaluated=true, BaseTypeSet=baseTypeSet))
        return baseTypeSet.AncestorTypes.Contains(supertype);

      false
    }

    protected override UnifyUnwraped(typeSymbol1 : TypeSymbol, typeSymbol2 : TypeSymbol, updatedVariables : ref LightList[TypeVarSymbol]) : bool
    {
      match (typeSymbol1, typeSymbol2)
      {
        | (FunctionTypeSymbol as f1, FunctionTypeSymbol as f2) =>
          unless (RequireAll(f1.Parameters, f2.Parameters, ref updatedVariables))
            return false;

          ProvideImpl(f1.Return, f2.Return, ref updatedVariables)

        | _ => base.UnifyUnwraped(typeSymbol1, typeSymbol2, ref updatedVariables)
      }
    }
  }
}
