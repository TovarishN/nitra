using Nitra;
using Nitra.Declarations;

using System.Collections.Immutable;

namespace DotNet
{
  abstract declaration Member : TypeMember
  {
    symbol
    {
      FullName = DeclaredIn.FullName + "." + this.Name;
    }

    | Constructor : Functional, CustomAttributeHostDeclaration, ModifierHostAndAst
      {
        symbol
        {
          Kind      = "constructor";
          SpanClass = DotNetLang.MethodSpanClass;

          ReturnType = DeclaredIn;
        }

        Symbol.Flags      |= Modifiers.Flags;
        Modifiers.Flags    = ModifierSet(context);
        Parameters.Scope   = Scope;

        Initializer : ConstructorInitializer?;
      }
    | Destructor : Parameterizable, CustomAttributeHostDeclaration, ModifierHostAndAst
      {
        symbol
        {
          Kind      = "destructor";
          SpanClass = DotNetLang.MethodSpanClass;
        }

        Parameters.Scope           = Scope;
      }
    | Method : Functional, GenericEntity, CustomAttributeHostDeclaration, ModifierHostAndAst
      {
        symbol
        {
          in IsExtensionMethod : bool;

          Kind      = "method";
          SpanClass = DotNetLang.MethodSpanClass;
        }

      stage 1:
        out ReturnTypeRef : Ref[TypeSymbol] = ReturnType.Ref.Resolve();

        InterfaceType.Scope      = Scope;
        ReturnType.Scope         = TypeParameterConstraints.Scope;
        Parameters.Scope         = TypeParameterConstraints.Scope;
        Symbol.ReturnType        = ReturnTypeRef.Symbol;
        Body.LocalVarsScopeIn    = TableScope(null, "LocalVars");
        Body.ParameterScope      = Symbol.ParameterScope;
        Body.ScopeIn             = Scope.HideWith(Symbol.ParameterScope).HideWith(Body.LocalVarsScopeIn);
        Symbol.IsExtensionMethod = AstUtils.IsExtensionMethod(Symbol.Parameters);
        HasExtensionMethodOut    = HasExtensionMethodIn || Symbol.IsExtensionMethod;

        InterfaceType : QualifiedReference?;
        IsPartial     : bool;
        ReturnType    : QualifiedReference;
        Body          : Statement;
      }
    | Property : CustomAttributeHostDeclaration, ModifierHostAndAst
      {
        symbol
        {
          Kind      = "property";
          SpanClass = DotNetLang.PropertySpanClass;

          in Setter : option[Member.MethodSymbol];
          in Getter : option[Member.MethodSymbol];
          in Type : TypeSymbol;
        }

      stage 1:
        out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

        InterfaceType.Scope  = Scope;
        Type.Scope           = Scope;
        Symbol.Type          = TypeRef.Symbol;
        Accessors.Scope      = Scope;

        //Initializer   : Expr?;
        InterfaceType : QualifiedReference?;
        Type          : QualifiedReference;
        Accessors     : PropertyAccessor*;
      }
    | Event : ModifierHost
      {
        symbol
        {
          Kind      = "event";
          SpanClass = DotNetLang.PropertySpanClass;

          in Type : TypeSymbol;
        }

        Header.Scope        = Scope;
        Symbol.Flags       |= Header.Modifiers.Flags;
        InterfaceType.Scope = Scope;
        Symbol.Type         = Header.TypeRef.Symbol;
        Accessors.Scope     = Scope;

        InterfaceType : QualifiedReference?;
        Header        : EventHeader;
        Accessors     : EventAccessor*;
      }
    | Indexer : Functional, CustomAttributeHostDeclaration, ModifierHostAndAst
      {
        symbol
        {
          Kind      = "indexer";
          SpanClass = DotNetLang.PropertySpanClass;
        }

      stage 1:
        out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

        InterfaceType.Scope        = Scope;
        Type.Scope                 = Scope;
        Parameters.Scope           = Scope;
        Symbol.ReturnType          = TypeRef.Symbol;
        Accessors.Scope            = Scope;

        Type          : QualifiedReference;
        InterfaceType : QualifiedReference?;
        Accessors     : PropertyAccessor*;
      }
    | Constant : ModifierHost
      {
        symbol
        {
          Kind      = "constant";
          SpanClass = DotNetLang.ConstantSpanClass;

          in Type : TypeSymbol;
        }

        Header.Scope  = Scope;
        Symbol.Flags |= Header.Modifiers.Flags;
        Symbol.Type   = Header.TypeRef.Symbol;

        Header : ConstantHeader;
      }
    | Field : ModifierHost
      {
        symbol
        {
          Kind      = "field";
          SpanClass = DotNetLang.FieldSpanClass;

          in Type : TypeSymbol;
        }

        Header.Scope  = Scope;
        Symbol.Flags |= Header.Modifiers.Flags;
        Symbol.Type   = Header.TypeRef.Symbol;

        Header : FieldHeader;
      }
    | FixedSizeBuffer : ModifierHost
      {
        symbol
        {
          Kind      = "fixed size buffer";
          SpanClass = DotNetLang.FieldSpanClass;

          in Type : TypeSymbol;
        }

        Header.Scope  = Scope;
        Symbol.Flags |= Header.Modifiers.Flags;
        Symbol.Type   = Header.TypeRef.Symbol;

        Header : FixedSizeBufferHeader;
      }
    | OperatorUnary : CustomAttributeHost, ModifierHostAndAst
      {
        symbol
        {
          Kind      = "operator";
          SpanClass = Language.OperatorSpanClass;

          table ParameterScope;
          in  ReturnType     : TypeSymbol;
        }

      stage 1:
        out ReturnTypeRef : Ref[TypeSymbol] = ReturnType.Ref.Resolve();

        ReturnType.Scope          = Scope;
        Parameter.ContainingTable = Symbol.ParameterScope;
        Parameter.Scope           = Scope;
        Parameter.IndexIn         = 0u;
        Symbol.ReturnType         = ReturnTypeRef.Symbol;

        ReturnType : QualifiedReference;
        Parameter  : FormalParameter;
      }
    | OperatorBinary : CustomAttributeHost, ModifierHostAndAst
      {
        symbol
        {
          Kind      = "operator";
          SpanClass = Language.OperatorSpanClass;

          table ParameterScope;
          in  ReturnType     : TypeSymbol;
        }

      stage 1:
        out ReturnTypeRef : Ref[TypeSymbol] = ReturnType.Ref.Resolve();

        ReturnType.Scope           = Scope;
        Parameter1.ContainingTable = Symbol.ParameterScope;
        Parameter1.Scope           = Scope;
        Parameter1.IndexIn         = 0u;
        Parameter2.ContainingTable = Symbol.ParameterScope;
        Parameter2.Scope           = Scope;
        Parameter2.IndexIn         = 1u;
        Symbol.ReturnType          = ReturnTypeRef.Symbol;

        ReturnType : QualifiedReference;
        Parameter1 : FormalParameter;
        Parameter2 : FormalParameter;
      }
    | OperatorCnv : CustomAttributeHost, ModifierHostAndAst
      {
        symbol
        {
          Kind      = "operator";
          SpanClass = Language.OperatorSpanClass;

          table ParameterScope;
          in  ReturnType     : TypeSymbol;
        }

      stage 1:
        out ReturnTypeRef : Ref[TypeSymbol] = ReturnType.Ref.Resolve();

        ReturnType.Scope          = Scope;
        Parameter.ContainingTable = Symbol.ParameterScope;
        Parameter.Scope           = Scope;
        Parameter.IndexIn         = 0u;
        Symbol.ReturnType         = ReturnTypeRef.Symbol;

        ReturnType : QualifiedReference;
        Parameter  : FormalParameter;
        Kind       : OperatorKind;
      }
  }

  abstract ast ConstructorInitializer
  {
    // TODO: Map arguments
    | This
    | Base
  }

  ast FieldHeader : CustomAttributeHost, ModifierAst
  {
  stage 1:
    out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

    Type.Scope = Scope;

    Type       : QualifiedReference;
  }

  ast ConstantHeader : CustomAttributeHost, ModifierAst
  {
  stage 1:
    out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

    Type.Scope = Scope;

    Type       : QualifiedReference;
  }

  ast EventHeader : CustomAttributeHost, ModifierAst
  {
  stage 1:
    out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

    Type.Scope = Scope;

    Type       : QualifiedReference;
  }

  ast FixedSizeBufferHeader : CustomAttributeHost, ModifierAst
  {
  stage 1:
    out TypeRef : Ref[TypeSymbol] = Type.Ref.Resolve();

    Type.Scope = Scope;

    Type       : QualifiedReference;
  }

  abstract declaration PropertyAccessor : CustomAttributeHostDeclaration, ModifierHostAndAst
  {
    // TODO: Map body
    | Get
    | Set
  }

  abstract declaration EventAccessor : CustomAttributeHostDeclaration
  {
    // TODO: Map body
    | Add
    | Remove
  }
}
