using Nitra.Declarations;
using Nitra.Serialization2;
using Nitra.Staging;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace DotNet
{
  public sealed class ModifierSet : ICollector, IEquatable[ModifierSet]
  {
    private this(modifiers : Modifiers)
    {
      _modifiers = modifiers;
    }

    public this(context : DependentPropertyEvalContext)
    {
      if (context != null)
        context.NotifyCollectorCreated(out CreatedOnStage, out CreatedOnPass);
      else
      {
        CreatedOnStage = -1;
        CreatedOnPass = -1;
      }
    }

    public CreatedOnStage           : int  { get; }
    public CreatedOnPass : int  { get; }
    public IsEmpty                  : bool { get { _modifiers == Modifiers.None } }

    private mutable _modifiers : Modifiers;

    [Collect]
    public Add(modifiers : Modifiers) : void
    {
      _modifiers |= modifiers;
    }

    [Collect]
    public Add(modifiers : ModifierSet) : void
    {
      _modifiers |= modifiers.GetModifiers();
    }

    public GetModifiers() : Modifiers
    {
      _modifiers
    }

    public Contains(modifiers : Modifiers) : bool
    {
      _modifiers %&& modifiers
    }

    public override ToString() : string
    {
      $"$CreatedOnStage($CreatedOnPass): $_modifiers"
    }

    public Equals(other : ModifierSet) : bool
      implements IEquatable[ModifierSet].Equals
    {
      | null => false
      | _    => this._modifiers == other._modifiers
    }

    public override Equals(other : object) : bool
    {
      | ModifierSet as other => Equals(other)
      | _                    => false
    }

    public override GetHashCode() : int
    {
      _modifiers :> int
    }

    public Serialize(writer : IO.BinaryWriter, _metadataWriter : MetadataWriter) : void
    {
      writer.Write(_modifiers :> int);
    }

    public static Deserialize(reader : IO.BinaryReader, _metadataReader : MetadataReader) : this
    {
      def modifiers = reader.ReadInt32() :> Modifiers;
      ModifierSet(modifiers)
    }
  }
}
