using Nitra.Internal.Recovery;
using Nitra.Runtime.Reflection;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using SCG =  System.Collections.Generic;

namespace Nitra.Internal
{
  public class TokenEnumerator
  {
    _parseResult : ParseResult;

    public this(parseResult : ParseResult) { _parseResult = parseResult; }

    _stack  : SCG.Stack[ParseFrame] = SCG.Stack();

    mutable _sequence      : SequenceInfo;
    mutable _subruleIndex  : int;
    mutable _rawTreePtr    : int;
    mutable _curTextPos    : int;
    mutable _listEndPos    : int;
    mutable _listSep       : bool;

    public Start(rawTreePtr : int, curTextPos : int) : TokenEnumerator
    {
      assert3(_parseResult != null);
      _stack.Clear();

      _sequence       = _parseResult.GetSequenceInfo(rawTreePtr);
      _rawTreePtr     = rawTreePtr;
      _subruleIndex   = -1;
      _curTextPos     = curTextPos;
      _listEndPos     = -1;
      this
    }

    public NextTokenSize() : int
    {
      _subruleIndex++;
      def rules = _sequence.Subrules;
      if (_subruleIndex >= rules.Length)
      {
        if (_stack.Count > 0)
        {
          (_sequence, _rawTreePtr, _subruleIndex, _curTextPos, _listEndPos, _listSep) = _stack.Pop();
          NextTokenSize()
        }
        else
          -1
      }
      else
      {
        def state = _parseResult.rawTree[_rawTreePtr + ExtensibleRuleParser.RawTreeOfs.State];
        def invalidState = state >= 0 && _sequence.Subrules[_subruleIndex].State >= state;
        def ruleInfo = rules[_subruleIndex];
        mutable fullSize;

        unless (invalidState)
        {
          def sizePtr = _rawTreePtr + ruleInfo.Offset;
          fullSize = _parseResult.GetSize(sizePtr);
          when (fullSize == 0)
            return NextTokenSize();
        }

        match (ruleInfo)
        {
          | ListItem => assert(false)
          | Empty =>
            if (invalidState)
              -1
            else
            {
              _curTextPos += fullSize;
              NextTokenSize()
            }

          | RegularCall
          | TokenString
          | ExtensibleCall as extensibleCall when extensibleCall.RuleParser.IsTokenRule
          | SimpleCall     as simpleCall     when simpleCall.RuleParser.IsTokenRule
          | List           as list           when list.IsOptimized
            =>
            if (invalidState)
              -1
            else
            {
              _curTextPos += fullSize;
              fullSize
            }

          | SimpleCall        (ruleParser) =>
            if (invalidState)
            {
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, int.MaxValue - 1, _curTextPos + fullSize, _listEndPos, _listSep));
              _rawTreePtr = _parseResult.TryGetRawTree(_curTextPos, ruleParser.RuleId);
              if (_rawTreePtr > 0)
              {
                _sequence     = _parseResult.GetSequenceInfo(_rawTreePtr);
                _subruleIndex = -1;
                _listEndPos   = -1;
                NextTokenSize()
              }
              else
                -1
            }
            else
            {
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, _subruleIndex, _curTextPos + fullSize, _listEndPos, _listSep));
              _rawTreePtr     = _parseResult.GetRawTree(_curTextPos, ruleParser.RuleId);
              _sequence       = _parseResult.GetSequenceInfo(_rawTreePtr);
              _subruleIndex   = -1;
              _listEndPos     = -1;
              NextTokenSize()
            }

          | ExtensibleCall(ruleParser)    =>
            if (invalidState)
            {
              _listEndPos     = -1;
              mutable prefixRawTreePtr = _parseResult.TryGetRawTree(_curTextPos, ruleParser.PrefixId);
              if (prefixRawTreePtr > 0)
              {
                def rawTreePtr      = _parseResult.GetPrefix(prefixRawTreePtr);
                if (rawTreePtr > 0)
                {
                  _stack.Push(ParseFrame(_sequence, _rawTreePtr, int.MaxValue - 1, _curTextPos, _listEndPos, _listSep));
                  _rawTreePtr     = rawTreePtr;
                  _sequence       = _parseResult.GetSequenceInfo(rawTreePtr);
                  _subruleIndex   = -1;
                  _listEndPos     = -1;
                  NextTokenSize()
                }
                else
                  -1;
              }
              else
                -1;
            }
            else if (fullSize == 0)
              NextTokenSize()
            else if (_listEndPos == -1)
            {
              _listEndPos     = _curTextPos + fullSize;
              mutable prefixRawTreePtr = _parseResult.GetRawTree(_curTextPos, ruleParser.PrefixId);
              def rawTreePtr      = ruleParser.BestPrefix(_parseResult.GetPrefix(prefixRawTreePtr), _parseResult);
              assert3(rawTreePtr > 0);
              def rule        = _parseResult.GetSequenceInfo(rawTreePtr);
              def rawTreeSize = rule.GetSize(rawTreePtr, _parseResult);
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, _subruleIndex - 1, _curTextPos + rawTreeSize, _listEndPos, _listSep));
              _rawTreePtr     = rawTreePtr;
              _sequence       = rule;
              _subruleIndex   = -1;
              _listEndPos     = -1;
              NextTokenSize()
            }
            else if (_curTextPos < _listEndPos)
            {
              mutable postfixRawTreePtr = _parseResult.GetRawTree(_curTextPos, ruleParser.PostfixId);
              def bestResult = ruleParser.BestPostfix(_parseResult.GetPostfix(postfixRawTreePtr), _parseResult);
              assert3(bestResult > 0);
              def rule        = _parseResult.GetSequenceInfo(bestResult);
              def rawTreeSize = rule.GetSize(bestResult, _parseResult);
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, _subruleIndex - 1, _curTextPos + rawTreeSize, _listEndPos, _listSep));
              _rawTreePtr     = bestResult;
              _sequence       = rule;
              _subruleIndex   = -1;
              _listEndPos     = -1;
              NextTokenSize()
            }
            else
            {
              assert3(_curTextPos == _listEndPos);
              _listEndPos     = -1;
              NextTokenSize()
            }

          | Option            as rule =>
            if (invalidState)
            {
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, int.MaxValue - 1, _curTextPos, _listEndPos, _listSep));
              _rawTreePtr = _parseResult.TryGetRawTree(_curTextPos, rule.Rule.Id);
              if (_rawTreePtr > 0)
              {
                _sequence     = _parseResult.GetSequenceInfo(_rawTreePtr);
                _subruleIndex = -1;
                _listEndPos   = -1;
                NextTokenSize()
              }
              else
                -1
            }
            else if (fullSize == 0)
              NextTokenSize()
            else
            {
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, _subruleIndex, _curTextPos + fullSize, _listEndPos, _listSep));
              _rawTreePtr     = _parseResult.GetRawTree(_curTextPos, rule.Rule.Id);
              _sequence       = _parseResult.GetSequenceInfo(_rawTreePtr);
              _subruleIndex   = -1;
              _listEndPos     = -1;
              NextTokenSize()
            }

          | List              as rule =>
            if (invalidState)
            {
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, int.MaxValue - 1, _curTextPos, _listEndPos, _listSep));
              _rawTreePtr = _parseResult.TryGetRawTree(_curTextPos, rule.Item.Id);
              if (_rawTreePtr > 0)
              {
                _sequence     = _parseResult.GetSequenceInfo(_rawTreePtr);
                _subruleIndex = -1;
                _listEndPos   = -1;
                NextTokenSize()
              }
              else
                -1
            }
            else if (fullSize == 0)
              NextTokenSize()
            else if (_listEndPos == -1)
            {
              _listEndPos     = _curTextPos + fullSize;
              def rawTreePtr  = _parseResult.GetRawTree(_curTextPos, rule.Item.Id);
              def rawTreeSize = rule.Item.GetSize(rawTreePtr, _parseResult);
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, _subruleIndex - 1, _curTextPos + rawTreeSize, _listEndPos, _listSep));
              _rawTreePtr     = rawTreePtr;
              _sequence       = _parseResult.GetSequenceInfo(_rawTreePtr);
              _listEndPos     = -1;
              _subruleIndex   = -1;
              NextTokenSize()
            }
            else if (_curTextPos < _listEndPos)
            {
              def rawTreePtr  = _parseResult.GetRawTree(_curTextPos, rule.Item.Id);
              def rawTreeSize = rule.Item.GetSize(rawTreePtr, _parseResult);
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, _subruleIndex - 1, _curTextPos + rawTreeSize, _listEndPos, _listSep));
              _rawTreePtr     = rawTreePtr;
              _sequence       = _parseResult.GetSequenceInfo(_rawTreePtr);
              _listEndPos     = -1;
              _subruleIndex   = -1;
              NextTokenSize()
            }
            else
            {
              assert3(_curTextPos == _listEndPos);
              _listEndPos     = -1;
              NextTokenSize()
            }

          | ListWithSeparator as rule =>
            if (invalidState)
            {
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, int.MaxValue - 1, _curTextPos, _listEndPos, _listSep));
              _rawTreePtr = _parseResult.TryGetRawTree(_curTextPos, rule.Item.Id);
              if (_rawTreePtr > 0)
              {
                _sequence     = _parseResult.GetSequenceInfo(_rawTreePtr);
                _subruleIndex = -1;
                _listEndPos   = -1;
                NextTokenSize()
              }
              else
                -1
            }
            else if (fullSize == 0)
              NextTokenSize()
            else if (_listEndPos == -1)
            {
              _listEndPos     = _curTextPos + fullSize;
              def rawTreePtr  = _parseResult.GetRawTree(_curTextPos, rule.Item.Id);
              def rawTreeSize = rule.Item.GetSize(rawTreePtr, _parseResult);
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, _subruleIndex - 1, _curTextPos + rawTreeSize, _listEndPos, true));
              _rawTreePtr     = rawTreePtr;
              _sequence       = _parseResult.GetSequenceInfo(_rawTreePtr);
              _listEndPos     = -1;
              _subruleIndex   = -1;
              NextTokenSize()
            }
            else if (_curTextPos < _listEndPos)
            {
              def curRule = if (_listSep) rule.Separator else rule.Item;
              def rawTreePtr  = _parseResult.GetRawTree(_curTextPos, curRule.Id);
              def rawTreeSize = curRule.GetSize(rawTreePtr, _parseResult);
              _stack.Push(ParseFrame(_sequence, _rawTreePtr, _subruleIndex - 1, _curTextPos + rawTreeSize, _listEndPos, !_listSep));
              _rawTreePtr     = rawTreePtr;
              _sequence       = _parseResult.GetSequenceInfo(_rawTreePtr);
              _listEndPos     = -1;
              _subruleIndex   = -1;
              NextTokenSize()
            }
            else
            {
              assert3(_curTextPos == _listEndPos);
              _listEndPos     = -1;
              NextTokenSize()
            }

          | Marker                   => assert3(false); //NextTokenSize()
        }
      }
    }
  }
}
