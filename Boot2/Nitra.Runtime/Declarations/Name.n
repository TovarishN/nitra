using Nitra.ProjectSystem;
using Nitra.Declarations;
using Nitra.Internal;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Declarations
{
  public partial class Name : AstBase
  {
    public this(location : Location, Id : long)
    {
      this(location);
      this.Id = Id;
    }

    public this(location : Location, Text : string)
    {
      this(location, StringIndex.GetId(Text));
    }

    public this(located : Located)
    {
      this(located.Location, located.GetText())
    }

    public virtual  Id   : long   { get; protected set; }
    public          Text : string { get { StringIndex.GetText(Id) } }

    public override ToString() : string { Text }
    public override ToXaml()   : string { AstUtils.XmlEscape(Text) }

    new public sealed partial class AmbiguousOrMissing : Name, IAmbiguousAst
    {
      public override Id : long
      {
        get
        {
          if (IsAmbiguous)
            Ambiguities[0].Id
          else
          {
            mutable id = base.Id;
            when (id == StringIndex.InvalidId)
            {
              id = StringIndex.GetId("#Missing");
              base.Id = id;
            }
            id
          }
        }
      }
    }
  }
}
