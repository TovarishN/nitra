using Nemerle;
using Nemerle.Utility;

using Nitra.Runtime;
using Nitra.ProjectSystem;
using Nitra.Typing;

using System;
using System.Reflection;
using System.IO;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Serialization
{
  public sealed class MetadataWriter : IMetadataWriter, IDisposable
  {
    public this(stream : Stream, project : Project)
    {
      this(stream, project, project.RootNamespace)
    }

    public this(stream : Stream, project : Project, root : OldNamespaceSymbol)
    {
      _stream             = stream;
      def encoding        = Text.Encoding.UTF8;
      _intermediateWriter = BinaryWriter(MemoryStream(), encoding);
      _symbolBodyWriter   = BinaryWriter(MemoryStream(), encoding);
      _root               = root;
      Project             = project;
    }

    private _stream             : Stream;
    private _intermediateWriter : BinaryWriter;
    private _symbolBodyWriter   : BinaryWriter;
    private _root               : OldNamespaceSymbol;
    private _namespaceNodes     : HashSet[HierarchyNode]              = HashSet();
    private _nonTreeSymbols     : HashSet[IExportableSymbol]          = HashSet();
    private _externalSymbols    : HashSet[IExportableSymbol]          = HashSet();
    private _nodeIds            : Dictionary[HierarchyNode,     int]  = Dictionary();
    private _symbolIds          : Dictionary[IExportableSymbol, int]  = Dictionary();
    private _assemblyIds        : Dictionary[AssemblyName,      byte] = Dictionary();
    private _serializationQueue : Queue[IExportableSymbol]            = Queue();
    private _stringTable        : Dictionary[string, int] = Dictionary();

    public Project : Project { get; }

    public Dispose() : void
    {
      _intermediateWriter.Dispose();
      _symbolBodyWriter.Dispose();
      _stream.Dispose();
    }

    public WriteSymbols() : void
    {
      def exportableSymbols = CollectExportableSymbols();
      WriteNameTree(exportableSymbols.Select(_.Node));
      WriteSymbolHeadersAndSerializeSymbols(exportableSymbols);
      WriteReferencedAssemblies();
      WriteNameTree(_externalSymbols.Select(_.Node));
      WriteExternalSymbolInfo();

      WriteStringsTableToSream();
      WriteBinaryWriterToSream(_intermediateWriter, _stream);
      WriteBinaryWriterToSream(_symbolBodyWriter, _stream);

      _stream.Flush();
    }

    private WriteStringsTableToSream() : void
    {
      def writer = BinaryWriter(_stream, Text.Encoding.UTF8, leaveOpen=true);

      writer.Write(NitraUtils.StringTableCheckData);

      // sorting...
      def strings = array(_stringTable.Count);
      foreach (kv in _stringTable)
        strings[kv.Value] = kv.Key;

      writer.Write(_stringTable.Count());

      foreach (str in strings)
        writer.Write(str);

      writer.Write(NitraUtils.StringTableCheckString);
    }

    private static WriteBinaryWriterToSream(writer : BinaryWriter, stream : Stream) : void
    {
      (writer.BaseStream :> MemoryStream).WriteTo(stream);
      writer.Dispose();
    }

    private WriteNameTree(nodes : IEnumerable[HierarchyNode]) : void
    {
      def write(node : HierarchyNode) : int
      {
        assert(node.Parent != null);

        mutable id;
        unless (_nodeIds.TryGetValue(node, out id))
        {
          def parentId = if (node.Parent : object != _root.Node) write(node.Parent) else -1;
          id = _nodeIds.Count + 1;
          def hasNamespaceSymbol = _namespaceNodes.Contains(node);
          _nodeIds.Add(node, id);
          _intermediateWriter.Write(id);
          _intermediateWriter.Write(parentId);
          _intermediateWriter.Write(node.PartName);
          _intermediateWriter.Write(hasNamespaceSymbol);
        }
        id
      }
      foreach (node in nodes)
        _ = write(node);
      _intermediateWriter.Write(0 : int);
    }

    private WriteSymbolHeadersAndSerializeSymbols(symbols : IEnumerable[IExportableSymbol]) : void
    {
      // tree symbols
      foreach (group in symbols.GroupBy(_.SymbolTypeId))
      {
        _intermediateWriter.Write(group.Key);
        foreach (symbol in group)
        {
          def symbolId = GetSymbolId(symbol);
          def nodeId   = GetNodeId(symbol);
          _intermediateWriter.Write(symbolId);
          _intermediateWriter.Write(nodeId);
          _serializationQueue.Enqueue(symbol);
        }
        _intermediateWriter.Write(0 : int);
      }

      // serialization of all symbols
      while (_serializationQueue.Count > 0)
      {
        def symbol   = _serializationQueue.Dequeue();
        def symbolId = GetSymbolId(symbol);
        _symbolBodyWriter.Write(symbolId);
        symbol.Serialize(_symbolBodyWriter, this);
      }

      _symbolBodyWriter.Write(0 : int);

      // non tree symbols fully populated only after actual serialization
      foreach (group in _nonTreeSymbols.GroupBy(_.SymbolTypeId))
      {
        _intermediateWriter.Write(group.Key);
        foreach (symbol in group)
        {
          def symbolId = GetSymbolId(symbol);
          def name     = symbol.Name;
          _intermediateWriter.Write(symbolId);
          _intermediateWriter.Write(0 : int);
          _intermediateWriter.Write(name);
        }
        _intermediateWriter.Write(0 : int);
      }

      _intermediateWriter.Write("");
    }

    private WriteReferencedAssemblies() : void
    {
      foreach (pair in _assemblyIds)
      {
        def assemblyId   = pair.Value;
        def assemblyName = pair.Key;
        _intermediateWriter.Write(assemblyId);
        _intermediateWriter.Write(assemblyName.FullName);
      }
      _intermediateWriter.Write(0 : byte);
    }

    private WriteExternalSymbolInfo() : void
    {
      foreach (group in _externalSymbols.GroupBy(_.SymbolTypeId))
      {
        _intermediateWriter.Write(group.Key);
        foreach (symbol in group)
        {
          def symbolId   = GetSymbolId(symbol);
          def nodeId     = GetNodeId(symbol);
          def assemblyId = GetAssemblyId(symbol);
          _intermediateWriter.Write(symbolId);
          _intermediateWriter.Write(nodeId);
          _intermediateWriter.Write(assemblyId);
        }
        _intermediateWriter.Write(0 : int);
      }
      _intermediateWriter.Write("");
    }

    private WriteSymbolRef(symbol : IExportableSymbol, writer : BinaryWriter) : void implements IMetadataWriter.WriteSymbolRef
    {
      if (null == symbol)
        writer.Write(0 : int);
      else if (symbol.Node == null)
      {
        unless (symbol.AssemblyId == AssemblyIds.CurrentAssembly)
          throw NotSupportedException("Serialization of referencies to external non tree symbols not supported");

        // локальный недревесный символ
        AddNonTreeSymbol(symbol);
        writer.Write(GetSymbolId(symbol));
      }
      else if (symbol.AssemblyId == AssemblyIds.CurrentAssembly)
      {
        // локальный древесный символ
        writer.Write(GetSymbolId(symbol));
      }
      else
      {
        // внешний древесный символ
        AddExternalSymbol(symbol);
        writer.Write(GetSymbolId(symbol));
      }
    }

    private WriteLocation(location : Location, writer : BinaryWriter) : void implements IMetadataWriter.WriteLocation
    {
      def span = location.Span;
      WriteString(location.Source.File.FullName, writer);
      writer.Write(span.StartPos);
      writer.Write(span.EndPos);
    }

    private WriteString(str : string, writer : BinaryWriter) : void implements IMetadataWriter.WriteString
    {
      mutable index : int;
      when (!_stringTable.TryGetValue(str, out index))
      {
        index = _stringTable.Count();
        _stringTable.Add(str, index);
      }
      writer.Write(index);
    }

    private CollectExportableSymbols() : List[IExportableSymbol]
    {
      def buffer = List();
      def visitNode(node)
      {
        foreach (symbol in node.GetSymbols())
        {
          when (symbol.AssemblyId == AssemblyIds.CurrentAssembly && symbol is IExportableSymbol)
            buffer.Add(symbol :> IExportableSymbol);
          when (symbol is OldNamespaceSymbol)
            _ = _namespaceNodes.Add(node);
        }
        foreach (node in node.SubNodes)
          visitNode(node);
      }
      visitNode(_root.Node);
      buffer
    }

    private AddNonTreeSymbol(symbol : IExportableSymbol) : void
    {
      when (_nonTreeSymbols.Add(symbol))
        _serializationQueue.Enqueue(symbol);
    }

    private AddExternalSymbol(symbol : IExportableSymbol) : void
    {
      when (_externalSymbols.Add(symbol))
      {
        def assemblyName = Project.GetAssemblyName(symbol.AssemblyId);
        unless (_assemblyIds.ContainsKey(assemblyName))
        {
          def id = (_assemblyIds.Count + 1) :> byte;
          _assemblyIds.Add(assemblyName, id);
        }
      }
    }

    private GetNodeId(symbol : IExportableSymbol) : int
    {
      _nodeIds[symbol.Node]
    }

    private GetSymbolId(symbol : IExportableSymbol) : int
    {
      mutable id;
      unless (_symbolIds.TryGetValue(symbol, out id))
      {
        id = _symbolIds.Count + 1;
        _symbolIds.Add(symbol, id);
      }
      id
    }

    private GetAssemblyId(symbol : IExportableSymbol) : byte
    {
      assert(symbol.AssemblyId != AssemblyIds.CurrentAssembly);
      assert(symbol.AssemblyId != AssemblyIds.NoAssembly);

      def assemblyName = Project.GetAssemblyName(symbol.AssemblyId);
      _assemblyIds[assemblyName]
    }
  }
}
