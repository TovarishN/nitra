using Nitra.ProjectSystem;
using Nitra.Declarations;
using Nitra.Staging;

using Nemerle;
using Nemerle.Imperative;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Reflection;
using System.IO;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.Serialization2
{
  public sealed class MetadataReader
  {
    private delegate DeserializationHandler(reader : BinaryReader, metadataReader : MetadataReader) : ISerializable;

    public Project       : Project                  { get; }
    public Source        : SourceSnapshot           { get; }
    public ErrorLocation : Location                 { get; }

    private _typeDescriptors         : Dictionary[int, MetadataTypeDescriptor] = Dictionary();
    private _deserializationHandlers : Dictionary[int, DeserializationHandler] = Dictionary();
    private _objects                 : Dictionary[int, ISerializable]          = Dictionary();

    public this(project : Project, source : SourceSnapshot)
    {
      Project       = project;
      Source        = source;
      ErrorLocation = Location(source, 0);
    }

    public ReadObject[T](reader : BinaryReader) : T
      where T: ISerializable
    {
      def descriptor = reader.ReadInt32();
      if (descriptor != 0)
      {
        def handler = GetDeserializationHandler(descriptor);
        def result  = handler.Invoke(reader, this);
        result :> T
      }
      else
        default(T)
    }

    public ReadType(reader : BinaryReader) : System.Type
    {
      def id = reader.ReadInt32();
      mutable desc;
      unless (_typeDescriptors.TryGetValue(id, out desc))
      {
        assert(false, $"Failed to find type descriptor with id='$id'. Try use UpdateStage1Metadata.cmd to update metadata.");
      }
      desc.Type;
    }

    public ReadObjects[T](reader : BinaryReader) : array[T]
      where T: ISerializable
    {
      def count  = reader.ReadInt32();
      def buffer = array(count);
      for (mutable i = 0; i < buffer.Length; ++i)
        buffer[i] = ReadObject(reader);
      buffer
    }

    public ReadRoot(root : DeclarationSymbol, stream : Stream) : void
    {
      try
      {
        _objects[MetadataDefaults.RootSymbolId] = root;
        _objects[MetadataDefaults.RootScopeId]  = root.Scope;

        def reader = BinaryReader(stream, MetadataDefaults.Encoding);
        def assemblyCount = reader.ReadInt32();
        for (mutable assemblyIndex = 0; assemblyIndex < assemblyCount; ++assemblyIndex)
        {
          def fullName = reader.ReadString();
          def assembly =
            try Project.ResolveSymbolAssembly(fullName)
            catch { e => throw ApplicationException($"Nitra MetadataReader v2.0 failed to load symbols from assembly '$fullName'. Exception: $(e.Message)", e); };

          def typeCount = reader.ReadInt32();
          for (mutable typeIndex = 0; typeIndex < typeCount; ++typeIndex)
          {
            def typeName = reader.ReadString();
            def id       = reader.ReadInt32();
            def argCount = reader.ReadInt32();
            def type     = LoadType(assembly, typeName);
            def descriptor =
              if (argCount > 0)
                MetadataTypeDescriptor.GenericDefinition(id, type, argCount)
              else
                MetadataTypeDescriptor.Simple(id, type);

            _typeDescriptors.Add(id, descriptor)
          }
        }

        def genericTypeCount = reader.ReadInt32();
        for (mutable typeIndex = 0; typeIndex < genericTypeCount; ++typeIndex)
        {
          def id            = reader.ReadInt32();
          def definitionId  = reader.ReadInt32();
          def definition    = _typeDescriptors[definitionId] :> MetadataTypeDescriptor.GenericDefinition;
          def arguments     = array(definition.ArgumentCount);
          def argumentTypes = array(arguments.Length);
          for (mutable index = 0; index < arguments.Length; ++index)
          {
            def argumentId       = reader.ReadInt32();
            def argument         = _typeDescriptors[argumentId];
            arguments[index]     = argument;
            argumentTypes[index] = argument.Type;
          }
          def type       = definition.Type.MakeGenericType(argumentTypes);
          def descriptor = MetadataTypeDescriptor.Generic(id, type, definition, arguments.ToNList());
          _typeDescriptors.Add(id, descriptor)
        }

        def symbolCount = reader.ReadInt32();
        repeat (symbolCount)
          _ = ReadObject(reader);
      }
      finally
      {
        _typeDescriptors.Clear();
        _deserializationHandlers.Clear();
        _objects.Clear();
      }
    }

    private LoadType(assembly : Assembly, typeName : string) : System.Type
    {
      def type = assembly.GetType(typeName, throwOnError = false);
      assert(type != null, $"Failed to find type '$typeName' from assembly '$assembly'.");
      type
    }

    private GetDeserializationHandler(id : int) : DeserializationHandler
    {
      mutable handler;
      unless (_deserializationHandlers.TryGetValue(id, out handler))
      {
        mutable desc;
        unless (_typeDescriptors.TryGetValue(id, out desc))
        {
          assert(false, $"Failed to find type descriptor with id='$id'. Try use UpdateStage1Metadata.cmd to update metadata.");
        }
        def type   = desc.Type;
        def method = type.GetMethod("Deserialize", BindingFlags.Public | BindingFlags.Static);
        assert(method != null, $"Failed to find method 'Deserialize' in type '$type'.");
        handler = Delegate.CreateDelegate(typeof(DeserializationHandler), method) :> DeserializationHandler;

        _deserializationHandlers.Add(id, handler);
      }
      handler
    }

    public BeginReadObject[T](reader : BinaryReader, obj : out T, factory : Func[BinaryReader, MetadataReader, T] = null) : bool
      where T: ISerializable
    {
      def id = reader.ReadInt32();
      if (id < 0)
      {
        def newObj = if (factory == null) Activator.CreateInstance() else factory(reader, this);
        _objects.Add(~id, newObj);
        obj = newObj;
        true
      }
      else
      {
        obj = _objects[id] :> T;
        false
      }
    }

    public BeginReadSymbol[TSymbol](reader : BinaryReader, obj : out TSymbol) : bool
      where TSymbol: DeclarationSymbol, new()
    {
      def id = reader.ReadInt32();
      if (id < 0)
      {
        def nameText        = reader.ReadString();
        def containingScope = ReadObject.[TableScope](reader);
        def symbolKind      = reader.ReadByte() :> SymbolKind;
        obj =
          match (symbolKind)
          {
            | WithoutDeclarations => TSymbol()
            | Parsed           with ignoreCase = false
            | ParsedIgnoreCase with ignoreCase = true =>
              def name        = Name(Location(Source, NSpan()), nameText);
              def declaration = ExternalDeclaration.[TSymbol](name, ignoreCase);
              declaration.DefineSymbol(containingScope)

            | External =>
              when (containingScope : object == null)
              {
                def msg = $"External symbol '$nameText' ($(typeof(TSymbol).FullName)) has no owner scope. This may be caused by adding a fake declaration to a symbol not added to the name table.";
                assert2(containingScope : object != null, msg);
                assert(containingScope : object != null, msg);
              }

              def reference  = Reference(Location(Source, NSpan()), nameText);
              def res        = containingScope.Bind.[TSymbol](reference);
              def candidates = res.GetSymbols();
              match (candidates.Length)
              {
                | 0 => assert(false, $"Failed to lookup reference to external symbol '$reference'");
                | 1 => candidates[0]
                | _ => assert(false, $"Ambiguous symbol reference '$reference'");
              }

            | x => assert(false, $"Unknown symbol kind '$x'.")
          };

        _objects.Add(~id, obj);
        symbolKind != SymbolKind.External
      }
      else
      {
        obj = _objects[id] :> TSymbol;
        false
      }
    }
  }
}
