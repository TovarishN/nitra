using DotNet;

using Nitra.AstUtils;
using Nitra.Declarations;

using System.Drawing;

namespace Nitra.Ast
{
  abstract ast LanguageMember : ScopedAst, BindableAst
  {
    inout ExtensionSet    : FileExtensionCollector;
  stage 1:
    inout SyntaxModuleSet : SyntaxModuleCollector;
    in DeclaringLanguage : LanguageSymbol;
  }

  declaration Style : LanguageMember
  {
    symbol
    {
      Kind      = "style";
      SpanClass = NitraLang.StyleSpanClass;

      in ForegroundColor : Color;
    stage 1:
      in DeclaringLanguage : LanguageSymbol;
    }

    Properties : StyleProperty*;

    decompose Properties
    {
      ForegroundColor : StyleProperty.ForegroundColor;
    }

    Symbol.ForegroundColor = ForegroundColor.Color.ValueOrDefault;
    Symbol.DeclaringLanguage = DeclaringLanguage;
  }

  declaration SpanClass : LanguageMember
  {
    symbol
    {
      Kind      = "span class";
      SpanClass = NitraLang.SpanClassSpanClass;

    stage 1:
      in DeclaringLanguage : LanguageSymbol;
      in Style             : StyleSymbol;
    }

  stage 1:
    out StyleRef : Ref[StyleSymbol] = StyleReference.Ref.Resolve();

    StyleReference.Scope = Scope;
    Symbol.DeclaringLanguage = DeclaringLanguage;
    Symbol.Style             = StyleRef.Symbol;

    StyleReference : QualifiedReference;
  }

  abstract ast SyntaxModuleReference : LanguageMember
  {
  stage 1:
    out ModuleRef : Ref[SyntaxModuleSymbol] = Module.Ref.Resolve();

    SyntaxModuleSetOut = SyntaxModuleSetIn.Add(this, ModuleRef, context);

    Module.Scope = Scope;

    Module : QualifiedReference;
  }

  ast AdditionSyntaxModuleReference : SyntaxModuleReference { }
  ast StartSyntaxModuleReference    : SyntaxModuleReference
  {
  stage 1:
    out RuleRef : Ref[SyntaxRuleSymbol] = Rule.Ref.Resolve();
    out RuleSymbol : SyntaxRuleSymbol = RuleRef.Symbol;

    Rule.Scope = ModuleRef.Scope;

    Rule : QualifiedReference;
  }

  ast FileExtension : LanguageMember
  {
    ExtensionSetOut = ExtensionSetIn.Add(this, context);

    Extension : string;
  }

  ast Company     : LanguageMember { Name : string; }
  ast Description : LanguageMember { Text : string; }
  ast Authors     : LanguageMember { Text : string; }
  ast Copyright   : LanguageMember { Text : string; }
  ast LicenseUrl  : LanguageMember { Text : string; }
  ast RequireLicenseAcceptance : LanguageMember { }

  abstract ast StyleProperty
  {
    | ForegroundColor { Color : Color; }
  }
}
