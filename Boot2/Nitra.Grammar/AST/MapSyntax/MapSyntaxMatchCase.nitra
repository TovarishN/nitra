using DotNet;

using Nitra.Declarations;

namespace Nitra.Ast
{
  abstract ast MapSyntaxMatchCase : BindableAst
  {
    out VariableTable : TableScope = TableScope("pattern variables");

  stage 1:
    in AstScope     : Scope;
    in PatternType  : TypeSymbol;
    in ExpectedType : TypeSymbol;

    Pattern.Scope           = Scope.HideWith(ParseTreeTypeCtorScope.Instance);
    Pattern.ContainingTable = VariableTable;
    Pattern.ExpectedType    = PatternType;

    Pattern : ParseTreePattern;

    | Inline
      {
        Code : string;
      }
    | Expression
      {
        Expression.AstScope     = AstScope;
        Expression.Scope        = Scope.HideWith(VariableTable);
        Expression.ExpectedType = AstUtils.Wait(Pattern.Type, ExpectedType);

        Expression : MapSyntaxExpression;
      }
  }
}
