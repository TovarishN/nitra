using Nitra.Utils;
using Nitra.Typing;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Text;
using Nemerle.Extensions;
using Nemerle.Utility;

using System;
using System.Text;
using System.Collections.Generic;
using System.Text.RegularExpressions;

using PExpr = Nemerle.Compiler.Parsetree.PExpr;

namespace Nitra.Model
{
  [Record, StructuralEquality]
  public variant RegularRule : Located
  {
    | Choice                  { rules    : list[RegularRule]; }
    | Sequence                { rules    : list[RegularRule]; }
    | Call                    { ruleRef  : SymbolRef[RegularRuleSymbol]; }
    | Repeat                  { minCount : int; maxCount : option[int]; rule : RegularRule; }
    | RepeatWithSeparator     { minCount : int; maxCount : option[int]; rule : RegularRule; separator : RegularRule; }
    | Subtract                { rule1 : RegularRule; rule2 : RegularRule; }
    | Chars
      {
        chars : list[RangeSet];

        [RecordIgnore, EqualsIgnore]
        private mutable _stringOpt : option[string];

        public StringOpt : option[string]
        {
          get
          {
            when (_stringOpt : object == null)
            {
              mutable buffer : StringBuilder;
              def loop(_)
              {
                | rangeSet :: others when rangeSet.IsSingleChar =>
                  when (buffer : object == null)
                    buffer = StringBuilder();
                  _ = buffer.Append(rangeSet.Ranges.Head.from);
                  loop(others)

                | _ :: _ => None()
                | [] when buffer : object == null => Some("")
                | []                              => Some(buffer.ToString())
              }
              _stringOpt = loop(chars)
            }
            _stringOpt;
          }
        }
      }

    public override Location : Location { get; }
    public Priority : int
    {
      get
      {
        match (this)
        {
          | Chars                       => 6
          | Call                        => 6
          | Repeat                      => 5
          | RepeatWithSeparator         => 5
          | Subtract                    => 3
          | Sequence                    => 2
          | Choice                      => 1
        }
      }
    }

    public override ToString() : string
    {
      def callHandler(buffer, callRule)
      {
        buffer.Append(callRule.ruleRef.ToString())
      }
      def stringHandler(buffer, str)
      {
        _ = buffer.Append("'");
        foreach (c in str)
          _ = buffer.Append(EscapeStringChar(c));
        buffer.Append("'")
      }
      StringBuilder().AppendRegularRule(this, callHandler, stringHandler, 0).ToString()
    }
  }

  public module RegularRuleExtensions
  {
    public AppendRegularRule(this buffer : StringBuilder,
      rule            : RegularRule,
      callHandler     : StringBuilder * RegularRule.Call -> StringBuilder,
      stringHandler   : StringBuilder * string -> StringBuilder,
      currentPriority : int) : System.Text.StringBuilder
    {
      def priority = rule.Priority;
      when (priority <= currentPriority)
        _ = buffer.Append("(");
      _ = match (rule)
      {
        | Call as call                                   => callHandler(buffer, call)
        | Choice(rules)                                  => buffer.AppendRegularRules(rules, callHandler, stringHandler, priority, " | ")
        | Sequence(rules)                                => buffer.AppendRegularRules(rules, callHandler, stringHandler, priority, " ")
        | Subtract(rule1, rule2)                         => buffer.AppendRegularRules([rule1, rule2], callHandler, stringHandler, priority, " - ")
        | Repeat(min, max, rule)                         => buffer.AppendRegularRule(rule, callHandler, stringHandler, priority).AppendRepeatQuantifier(min, max)
        | RepeatWithSeparator(min, max, rule, separator) => buffer.Append('(').AppendRegularRule(rule, callHandler, stringHandler, 0).Append(", ").AppendRegularRule(separator, callHandler, stringHandler, 0).Append(")").AppendRepeatQuantifier(min, max)
        | Chars(StringOpt = Some(str))                   => stringHandler(buffer, str)
        | Chars([rangeSet])                              => buffer.AppendRangeSet(rangeSet)
        | Chars(rangeSets)                               =>
          _ = buffer.Append('(');
          foreach (rangeSet in rangeSets with index)
          {
            when (index > 0)
              _ = buffer.Append(" | ");
            _ = buffer.AppendRangeSet(rangeSet)
          }
          buffer.Append(')')
      }
      when (priority <= currentPriority)
        _ = buffer.Append(")");
      buffer
    }

    private AppendRepeatQuantifier(this buffer : StringBuilder, min : int, max : option[int]) : StringBuilder
    {
      match (min, max)
      {
        | (0, None)                        => buffer.Append('*')
        | (1, None)                        => buffer.Append('+')
        | (min, None)                      => buffer.Append('{').Append(min).Append(",}")
        | (0, Some(1))                     => buffer.Append('?')
        | (min, Some(max)) when min == max => buffer.Append('{').Append(min).Append('}')
        | (min, Some(max))                 => buffer.Append('{').Append(min).Append(',').Append(max).Append('}')
      }
    }

    public AppendRegularRules(this buffer : System.Text.StringBuilder,
      rules           : list[RegularRule],
      callHandler     : StringBuilder * RegularRule.Call -> StringBuilder,
      stringHandler   : StringBuilder * string -> StringBuilder,
      currentPriority : int,
      separator       : string) : System.Text.StringBuilder
    {
      foreach (rule in rules with index)
      {
        when (index > 0)
          _ = buffer.Append(separator);
        _ = buffer.AppendRegularRule(rule, callHandler, stringHandler, currentPriority)
      }
      buffer
    }

    public AppendRangeSet(this buffer : StringBuilder, chars : RangeSet) : StringBuilder
    {
      buffer.Append('[').Append(chars.ToString()).Append(']')
    }
  }
}
