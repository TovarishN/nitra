//#define MEASUREMENT
//#define LAZY
using DotNet;

using Microsoft.Cci;

using Nemerle;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Text;
using Nemerle.Utility;

using Nitra.Declarations;
using Nitra.Internal;
using Nitra.Serialization2;

using System;
using System.Diagnostics;
using System.IO;
using System.Collections.Generic;
using System.Linq;

namespace Nitra.BackEnd.Cci
{
  public class TypeMemberScope : TableScope
  {
#if MEASUREMENT
    private static mutable _materializeMembersTime : TimeSpan;
    private static mutable _materializeMemberCount : int;
    private static mutable _loadMembersTime        : TimeSpan;
    private static mutable _loadMemberTypeCount    : int;
    private static _timer                          : Stopwatch = Stopwatch.StartNew();

    public static MaterializeMembersTime : TimeSpan { get { _materializeMembersTime } }
    public static LoadMembersTime        : TimeSpan { get { _loadMembersTime } }
    public static MaterializeMemberCount : int { get { _materializeMemberCount } }
    public static LoadMemberTypeCount    : int { get { _loadMemberTypeCount } }
    public static ResetTime() : void
    {
      _materializeMembersTime = _loadMembersTime     <- default(TimeSpan);
      _materializeMemberCount = _loadMemberTypeCount <- 0;
    }
#endif

    _cciBackEnd               : CciBackEnd;
    mutable _isLoaded         : bool;
#if LAZY
    mutable _backundNameTable : Hashtable[int, list[ITypeDefinitionMember]];
#endif

    public this(owner : ContainerSymbol, name : string, cciBackEnd : CciBackEnd)
    {
      base(owner, name);
      _cciBackEnd = cciBackEnd;
    }

#if LAZY
    Init() : void
    {
      _backundNameTable = Hashtable();
    }
#endif

    Load() : void
    {
      _isLoaded = true;
#if LAZY
      Init();
#endif

      def context = DependentPropertyEvalContext(null);
      def owner = Owner :> ContainerSymbol;
      def type = (owner.FirstDeclarationOrDefault :> IExternalTypeDeclaration).Type;
      def props = List();
      def methods = Dictionary();

      def getTypeScope(type : TypeSymbol)
      {
        | x is TopConstructedTypeSymbol => x.TypeInfo.Scope
        | null => assert2(false); EmptyScope.Instance
        | _ => type.Scope
      }

#if MEASUREMENT
      _timer.Restart();
#endif
      def members : array[ITypeDefinitionMember] = type.Members.ToArray();
#if MEASUREMENT
      _loadMemberTypeCount++;
      _materializeMemberCount += members.Length;
#endif
#if LAZY
      foreach (m in members)
      {
        def id = StringIndex.GetId(m.Name.Value);
        mutable backendSymbols;
        unless (_backundNameTable.TryGetValue(id, out backendSymbols))
          backendSymbols = [];
        _backundNameTable[id] = m :: backendSymbols;
      }
#endif
#if MEASUREMENT
      _materializeMembersTime += _timer.Elapsed;
      _timer.Restart();
#endif

      foreach (m in members)
      {
        | _ is ITypeDefinition => ()
        | member =>
          def symbol = _cciBackEnd.LoadTypeMembers(member, owner, context);

          match (member, symbol)
          {
            | (member is IPropertyDefinition, propSymbol is Member.PropertySymbol) =>
              // Remember property for getter/setter resolution
              props.Add((member, propSymbol));

              // Link Type scope to property Scope
              propSymbol.Scope = getTypeScope(propSymbol.Type);
            | (_ is IFieldDefinition, fieldSymbol is Member.FieldSymbol) =>
              // Link Type scope to field Scope
              fieldSymbol.Scope = getTypeScope(fieldSymbol.Type);
            | (_, symbol is Member.MethodSymbol) =>
              // Save methods for getter/setter resolution
              // don't care about overloads, since this is for properties
              methods[member.Name.UniqueKey] = symbol;
              symbol.Scope = getTypeScope(symbol.ReturnType);
            | _ => ()
          }
      }

      foreach((propDef, propSym) in props) {
        propSym.Getter = if (propDef.Getter != null) Some(methods[propDef.Getter.Name.UniqueKey]) else None();
        propSym.Setter = if (propDef.Setter != null) Some(methods[propDef.Setter.Name.UniqueKey]) else None();
      }

#if MEASUREMENT
      _loadMembersTime += _timer.Elapsed;
#endif
    }

    public override GetAllSymbols() : System.Collections.Generic.IEnumerable[Nitra.Declarations.DeclarationSymbol]
    {
      unless (_isLoaded)
        Load();
      base.GetAllSymbols()
    }

    public override Symbols : Seq[list[DeclarationSymbol]]
    {
      get
      {
        unless (_isLoaded)
          Load();
        base.Symbols
      }
    }

    public override Define[TSymbol](declaration : Declaration, context : DependentPropertyEvalContext, name : Name = null) : TSymbol
    {
      base.Define(declaration, context, name)
    }

    public override Bind[TSymbol](isMatch : Predicate[TSymbol]) : BindResult[TSymbol]
    {
      unless (_isLoaded)
        Load();
      base.Bind(isMatch)
    }

    public override Bind[TSymbol](reference : Reference) : BindResult[TSymbol]
    {
      unless (_isLoaded)
        Load();
      base.Bind(reference)
    }

    public override ToDot(nodeToIdMap : Dictionary[object, int], nodes : List[string], _edges : List[string]) : void
    {
      unless (_isLoaded)
        Load();
      base.ToDot(nodeToIdMap, nodes, _edges)
    }

    public override Serialize(writer : BinaryWriter, metadataWriter : MetadataWriter) : void
    {
      IgnoreParams();
      assert(false)
    }

    public static new Deserialize(_reader : BinaryReader, _metadataReader : MetadataReader) : this
    {
      IgnoreParams();
      assert(false, "TypeMemberScope must be used only in backend")
    }
  }
}
