using Nitra;
using Nitra.Declarations;

using DotNet;

using Nemerle.Collections;

namespace CSharp
{
  map syntax Main.CompilationUnit -> CompilationUnit
  {
    NamespaceMemberDeclarations -> Members;
  }

  map syntax Types.TypeParameter -> TypeParameterAlias
  {
    Name -> Name;
    Variance =
      if (VarianceAnnotationOpt.HasValue)
        ParsedValue(VarianceAnnotationOpt.Value.Annotation, if (VarianceAnnotationOpt.Value.Annotation.Length == 2) Variance.Covariant else Variance.Contravariant)
      else ParsedValue(VarianceAnnotationOpt.Span, Variance.None);
    Attributes -> Attributes;
  }

  map syntax Types.TypeParameters -> TypeParameterAlias*
  {
    TypeParameters.Item1
  }

  map syntax Types.TypeParameterConstraint -> TypeParameterConstraint
  {
    | Ctor   {}
    | Class  {}
    | Struct {}
    | Base { AnyType -> Type; }
  }

  map syntax Types.TypeParameterConstraints -> TypeParameterConstraint*
  {
    TypeParameterConstraints.Item1
  }

  map syntax Types.TypeParameterConstraintsClause -> TypeParameterConstraintsClause
  {
    Reference                -> ParameterName;
    TypeParameterConstraints -> Constraints;
  }

  map syntax Types.TypeParameterConstraintsClauses -> TypeParameterConstraintsClause*
  {
    TypeParameterConstraintsClauses
  }

  map syntax TopDeclarations.TypeDeclaration -> NamespaceMember
  {
    | Class -> TopClass
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Struct -> TopStruct
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Interface -> TopInterface
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Enum -> TopEnum
      {
        Name        -> Name;
        EnumBaseOpt -> EnumBase;
        EnumBody.EnumMemberDeclarations -> Members;
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Delegate -> TopDelegate
      {
        Name             -> Name;
        AnyType          -> ReturnType;
        FormalParameters.Item1 -> Parameters;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
  }

  map syntax TopDeclarations.TypeDeclaration(_tag : NestedTypeTag) -> TypeMember
  {
    | Class -> NestedClass
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Struct -> NestedStruct
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Interface -> NestedInterface
      {
        Name                            -> Name;
        TypeBody.TypeMemberDeclarations -> Members;
        match(TypeBaseOpt)       { Some(value) -> value | None() -> [] } -> TypeBase;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Enum -> NestedEnum
      {
        Name        -> Name;
        EnumBaseOpt -> EnumBase;
        EnumBody.EnumMemberDeclarations -> Members;
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
    | Delegate -> NestedDelegate
      {
        Name             -> Name;
        AnyType          -> ReturnType;
        FormalParameters.Item1 -> Parameters;
        match(TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        TypeParameterConstraintsClauses -> TypeParameterConstraints;
        Attributes -> Attributes;
        Modifiers  -> Modifiers;
      }
  }

  map syntax TopDeclarations.TypeBase -> QualifiedReference*
  {
    AnyTypes.Item1
  }

  map syntax CS6.TypeMemberDeclaration.PropertyWithInitializer -> TypeMember*
  {
    [Member.Property
    {
      Name             -> Name;
      AnyType          -> Type;
      InterfaceTypeOpt -> InterfaceType;
      PropertyBody.AccessorDeclarations -> Accessors;
      Attributes       -> Attributes;
      Modifiers        -> Modifiers;
    }]
  }

  map syntax CS6.TypeMemberDeclaration.PropertyExpressionBodied -> TypeMember*
  {
    [Member.Property
    {
      Name             -> Name;
      AnyType          -> Type;
      InterfaceTypeOpt -> InterfaceType;
      [] -> Accessors; // TODO: mage getter from Expression
      Attributes       -> Attributes;
      Modifiers        -> Modifiers;
    }]
  }

  map syntax CS6.TypeMemberDeclaration.MethodExpressionBodied -> TypeMember*
  {
    [Member.Method
      {
        MethodHeader.Name             -> Name;
        MethodHeader.InterfaceTypeOpt -> InterfaceType;
        match(MethodHeader.TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        MethodHeader.TypeParameterConstraintsClauses -> TypeParameterConstraints;
        IsPartial = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
        MethodHeader.AnyType          -> ReturnType;
        MethodHeader.FormalParameters.Item1 -> Parameters;
        Attributes                    -> Attributes;
        Modifiers                     -> Modifiers;
        DotNet.ExpressionStatement { Expression -> Expr; } -> Body;
      }]
  }

  map syntax TopDeclarations.TypeMemberDeclaration -> TypeMember*
  {
    | NestedType      -> [TypeDeclaration(NestedTypeTag {})]
    | Constructor     -> [Member.Constructor
      {
        Name                      -> Name;
        FormalParameters.Item1    -> Parameters;
        Attributes                -> Attributes;
        Modifiers                 -> Modifiers;
        ConstructorInitializerOpt -> Initializer;
      }]
    | Destructor      -> [Member.Destructor
      {
        Name             -> Name;
        FormalParameters.Item1 -> Parameters;
        Attributes       -> Attributes;
        Modifiers        -> Modifiers;
      }]
    | Method          -> [Member.Method
      {
        match(MethodHeader.TypeParametersOpt) { Some(value) -> value | None() -> [] } -> TypeParameterAliases;
        MethodHeader.Name                             -> Name;
        MethodHeader.InterfaceTypeOpt                 -> InterfaceType;
        MethodHeader.TypeParameterConstraintsClauses  -> TypeParameterConstraints;
        MethodHeader.AnyType                          -> ReturnType;
        MethodHeader.FormalParameters.Item1           -> Parameters;
        Attributes                                    -> Attributes;
        Modifiers                                     -> Modifiers;
        Body                                          -> Body;
        IsPartial                                     = ParsedValue(PartialOpt.Span, PartialOpt.HasValue);
      }]
    | Property        -> [Member.Property
      {
        Name             -> Name;
        AnyType          -> Type;
        InterfaceTypeOpt -> InterfaceType;
        PropertyBody.AccessorDeclarations -> Accessors;
        Attributes       -> Attributes;
        Modifiers        -> Modifiers;
      }]
    | Event           -> [Member.Event
      {
        Name             -> Name;
        InterfaceTypeOpt -> InterfaceType;
        EventHeader { AnyType -> Type; Attributes -> Attributes; Modifiers -> Modifiers; } -> Header;
        EventAccessorDeclarations -> Accessors;
      }]
    | Indexer         -> [Member.Indexer
      {
        Name = Name(this.Location.WithSpan(this.KwThis), "#Item");
        FormalParameters.Item1 -> Parameters;
        AnyType          -> Type;
        InterfaceTypeOpt -> InterfaceType;
        PropertyBody.AccessorDeclarations -> Accessors;
        Attributes       -> Attributes;
        Modifiers        -> Modifiers;
      }]
    | SimpleEvent     -> VariableDeclarators       .Item1(EventHeader           { AnyType -> Type; Attributes -> Attributes; Modifiers -> Modifiers; })
    | Field           ->
        let header : FieldHeader = FieldHeader { AnyType -> Type; Attributes -> Attributes; Modifiers -> Modifiers; } in
        match (VariableDeclarators.Item1)
        {
          | []    -> [Member.Field { Name = Name(this.Location, "field"); header -> Header; }]
          | decls -> decls(header)
        }

    | Constant        -> ConstantDeclarators       .Item1(ConstantHeader        { AnyType -> Type; Attributes -> Attributes; Modifiers -> Modifiers; })
    | FixedSizeBuffer -> FixedSizeBufferDeclarators.Item1(FixedSizeBufferHeader { AnyType -> Type; Attributes -> Attributes; Modifiers -> Modifiers; })
    | OperatorUnary   -> [Member.OperatorUnary
      {
        Name = Name(this.Location.WithSpan(this.OverloadableUnaryOperator), this.GetText(OverloadableUnaryOperator));
        FormalParameter -> Parameter;
        AnyType         -> ReturnType;
        Attributes      -> Attributes;
        Modifiers       -> Modifiers;
      }]
    | OperatorBinary  -> [Member.OperatorBinary
      {
        Name = Name(this.Location.WithSpan(this.OverloadableBinaryOperator), this.GetText(OverloadableBinaryOperator));
        FormalParameter1 -> Parameter1;
        FormalParameter2 -> Parameter2;
        AnyType          -> ReturnType;
        Attributes       -> Attributes;
        Modifiers        -> Modifiers;
      }]
    | ImplicitOperator   -> [Member.OperatorCnv
      {
        Name = Name(this.Location.WithSpan(this.KwImplicit), "#Implicit");
        FormalParameter -> Parameter;
        AnyType         -> ReturnType;
        Kind = ParsedValue(this.KwImplicit, OperatorKind.Implicit);
        Attributes      -> Attributes;
        Modifiers       -> Modifiers;
      }]
    | ExplicitOperator   -> [Member.OperatorCnv
      {
        Name = Name(this.Location.WithSpan(this.KwExplicit), "#Explicit");
        FormalParameter -> Parameter;
        AnyType         -> ReturnType;
        Kind = ParsedValue(this.KwExplicit, OperatorKind.Explicit);
        Attributes      -> Attributes;
        Modifiers       -> Modifiers;
      }]
  }

  map syntax Statements.VariableDeclarator(header : FieldHeader) -> Member.Field
  {
    Name   -> Name;
    header -> Header;
  }

  map syntax Statements.VariableDeclarator(header : EventHeader) -> Member.Event
  {
    Name   -> Name;
    header -> Header;
    None() -> InterfaceType;
    []     -> Accessors;
  }

  map syntax TopDeclarations.EnumBase -> QualifiedReference
  {
    AnyType
  }

  map syntax TopDeclarations.EnumMemberDeclaration -> EnumMember
  {
    Name       -> Name;
    Attributes -> Attributes;
  }

  map syntax TopDeclarations.EnumMemberDeclarations -> EnumMember*
  {
    EnumMemberDeclarations.Item1
  }

  map syntax Core.NamespaceMemberDeclaration -> NamespaceMember
  {
  }

  map syntax TopDeclarations.NamespaceMemberDeclaration.Namespace -> NamespaceMember
  {
    ExplicitNamespace
    {
      NamespaceName.Name          -> Name;
      NamespaceName.Path(ImplicitNamespaceTag{}) -> Path;
      //ExternAliasDirectives       -> ExternAlias;
      //UsingDirectives             -> UsingDirectives;
      NamespaceMemberDeclarations -> Members;
    }
  }

  map syntax TopDeclarations.NamespaceMemberDeclaration.Type -> NamespaceMember
  {
    TypeDeclaration
  }

  map syntax TopDeclarations.NamespaceMemberDeclaration.ExternAliasDirective -> NamespaceMember
  {
    ExternAliasDirective
    {
      Name -> Name;
    }
  }

  map syntax TopDeclarations.NamespaceMemberDeclaration.Alias -> NamespaceMember
  {
    UsingAliasDirective
    {
      Name          -> Name;
      QualifiedName -> NamespaceOrTypeName;
    }
  }

  map syntax TopDeclarations.NamespaceMemberDeclaration.Open -> NamespaceMember
  {
    UsingOpenDirective
    {
      QualifiedName -> NamespaceOrTypeName;
    }
  }

  map syntax TopDeclarations.NamespaceMemberDeclaration.GlobalAttributeSection -> NamespaceMember
  {
    GlobalAttributes
    {
      GlobalAttributeSection -> Attributes;
    }
  }

  ast UsingStaticDirective : NamespaceMember
  {
  }

  map syntax CS6.NamespaceMemberDeclaration.OpenStatic -> NamespaceMember
  {
    UsingOpenDirective
    {
      QualifiedName -> NamespaceOrTypeName;
    }
  }

  //map syntax CS6.NamespaceMemberDeclaration.UsingStaticDirective -> NamespaceMember
  //{
  //  UsingStaticDirective
  //  {
  //  }
  //}

  ast UsingSyntax : NamespaceMember
  {
  }

  map syntax Extensible.NamespaceMemberDeclaration.UsingSyntax -> NamespaceMember
  {
    UsingSyntax
    {
    }
  }

  map syntax CsIdentifiers.Reference -> Nitra.Declarations.Reference
  {
    | Normal   = Nitra.Declarations.Reference(this);
    | Verbatim = Nitra.Declarations.Reference(this.Location, GetText(this.IdentifierBody));
  }

  map syntax CsIdentifiers.Name -> Nitra.Declarations.Name
  {
    | Normal   = Nitra.Declarations.Name(this);
    | Verbatim = Nitra.Declarations.Name(this.Location, GetText(this.IdentifierBody));
  }

  ast ImplicitNamespaceTag { }

  map syntax CsIdentifiers.Name(_tag : ImplicitNamespaceTag) -> ImplicitNamespace
  {
    | Normal   -> ImplicitNamespace { this -> Name; }
    | Verbatim -> ImplicitNamespace { this -> Name; }
  }

  map syntax TopDeclarations.ConstructorInitializer -> ConstructorInitializer
  {
    | This {}
    | Base {}
  }

  map syntax TopDeclarations.FormalParameter -> FormalParameter
  {
    Name       -> Name;
    AnyType    -> Type;
    Attributes -> Attributes;
    match (DefaultArgumentOpt) { Some(value) -> value.Expression | None -> Expr.Nop {} } -> Default;
    Modifier =
      if (this.ParameterModifierOpt.HasValue)
      {
        def result =
          match (this.GetText(this.ParameterModifierOpt.Value))
          {
            | "ref"    => ParameterModifier.Ref
            | "out"    => ParameterModifier.Out
            | "this"   => ParameterModifier.This
            | "params" => ParameterModifier.Params
            | _        => assert2(false, "Unknown parameter modifier value"); ParameterModifier.None
          };
        ParsedValue(this.ParameterModifierOpt.Value, result)
      }
      else
        ParsedValue(this.ParameterModifierOpt.Span, ParameterModifier.None);
  }

  map syntax Statements.ConstantDeclarator(header : ConstantHeader) -> Member.Constant
  {
    Name -> Name;
    header -> Header;
  }

  map syntax TopDeclarations.FixedSizeBufferDeclarator(header : FixedSizeBufferHeader) -> Member.FixedSizeBuffer
  {
    Name   -> Name;
    header -> Header;
  }

  map syntax TopDeclarations.GetAccessorDeclaration -> PropertyAccessor.Get
  {
    Name = Name(this.Location.WithSpan(this.KwGet), "#Get");
    Attributes -> Attributes;
    Modifiers  -> Modifiers;
  }

  map syntax TopDeclarations.SetAccessorDeclaration -> PropertyAccessor.Set
  {
    Name = Name(this.Location.WithSpan(this.KwSet), "#Set");
    Attributes -> Attributes;
    Modifiers  -> Modifiers;
  }

  map syntax TopDeclarations.AccessorDeclarations -> PropertyAccessor*
  {
    | GetSet -> match (this) { (g, Some(s)) -> [g, s] | (g, None) -> [g] }
    | SetGet -> match (this) { (s, Some(g)) -> [s, g] | (s, None) -> [s] }
    | None   -> []
  }

  map syntax TopDeclarations.AddAccessorDeclaration -> EventAccessor.Add
  {
    Name = Name(this.Location.WithSpan(this.KwAdd), "#Add");
    Attributes -> Attributes;
  }

  map syntax TopDeclarations.RemoveAccessorDeclaration -> EventAccessor.Remove
  {
    Name = Name(this.Location.WithSpan(this.KwRemove), "#Remove");
    Attributes -> Attributes;
  }

  map syntax TopDeclarations.EventAccessorDeclarations -> EventAccessor*
  {
    | AddRemove -> [AddAccessorDeclaration, RemoveAccessorDeclaration]
    | RemoveAdd -> [RemoveAccessorDeclaration, AddAccessorDeclaration]
  }

  map syntax Core.Modifiers -> Modifier*
  {
    Modifiers
  }

  map syntax Core.Modifier -> Modifier
  {
    | New       {}
    | Public    {}
    | Protected {}
    | Internal  {}
    | Private   {}
    | Virtual   {}
    | Volatile  {}
    | Static    {}
    | Readonly  {}
    | Sealed    {}
    | Override  {}
    | Abstract  {}
    | Extern    {}
  }

  map syntax Unsafe.Modifier.Unsafe -> Modifier
  {
    Unsafe    {}
  }

  map syntax Async.Modifier.Async -> Modifier
  {
    Async    {}
  }

  map syntax CsIdentifiers.QualifiedIdentifier -> QualifiedReference
  {
    | Aliased -> Aliased { Alias -> Alias; QualifiedReferenceParts -> QualifiedName; }
    | Normal -> QualifiedReferenceParts
  }

  map syntax CsIdentifiers.QualifiedReferenceParts -> QualifiedReference
  {
    | Simple      -> Simple    { Reference -> Name; }
    | Qualified   -> Qualified { QualifiedReferenceParts -> Qualifier;     Reference      -> Name; }
    | GenericType -> Generic   { QualifiedReferenceParts -> QualifiedName; TypeArgs.Item1 -> Arguments; }
    | OpenType    -> Open      { QualifiedReferenceParts -> QualifiedName; Rank = ParsedValue(RankSeparators.Span, RankSeparators.Count); }
  }


  //map syntax CsIdentifiers.QualifiedIdentifier -> QualifiedReference
  //{
  //  match (this)
  //  {
  //    | (None,    (first :: other, _)) -> fold (item in other; acc = first();    acc = item(acc))
  //    | (Some(a), (first :: other, _)) -> fold (item in other; acc = first(a()); acc = item(acc))
  //    | (_,       ([],             _)) = AstUtils.CreateMissingQualifiedReference(QualifiedIdentifierParts.Location);
  //  }
  //}

  map syntax CsIdentifiers.QualifiedIdentifierPartTypeArguments -> QualifiedReference*
  {
    | TypeArgumentList -> AnyTypes.Item1
    | OpenTypeArguments =
      {
        def args = array(RankSeparators.Count + 1);
        args[0] = AstUtils.CreateSimpleName(this.Location.WithSpan(NSpan(OpenBrace.EndPos)), "#");
        foreach (rs in RankSeparators with index)
          args[index + 1] = AstUtils.CreateSimpleName(this.Location.WithSpan(NSpan(rs.EndPos)), "#");
        QualifiedReference.AstList(this.Location, args)
      }
  }

  map syntax CsIdentifiers.QualifiedIdentifierPart -> QualifiedReference
  {
    let qid : QualifiedReference = Simple { Reference -> Name; } in
    match (QualifiedIdentifierPartTypeArgumentsOpt)
    {
      | None -> qid
      | Some(typeArgs) -> Generic { qid -> QualifiedName; typeArgs -> Arguments; }
    }
  }

  map syntax CsIdentifiers.QualifiedIdentifierPart(qualifier : QualifiedReference) -> QualifiedReference
  {
    let qid : QualifiedReference = Qualified { Reference -> Name; qualifier -> Qualifier; } in
    match (QualifiedIdentifierPartTypeArgumentsOpt)
    {
      | None -> qid
      | Some(typeArgs) -> Generic { qid -> QualifiedName; typeArgs -> Arguments; }
    }
  }

  map syntax Types.TypeName -> QualifiedReference
  {
    | QualifiedName  -> QualifiedIdentifier
    | PredefinedType -> Simple { Name = Reference(this.Location, GetText(PredefinedType)); }
  }

  map syntax Types.AnyNonArrayType -> QualifiedReference
  {
    match (this)
    {
      | (typeName, None) -> typeName
      | (typeName, Some) -> Generic
        {
          Qualified
          {
            Simple { Name = Reference(this.Location, "System"); } -> Qualifier;
            Name = Reference(this.Location, "Nullable");
          } -> QualifiedName;
          [ typeName ] -> Arguments;
        }
    }
  }

  map syntax Types.AnyNonArrayTypeNullableHack -> QualifiedReference
  {
    match (this)
    {
      | (typeName, None) -> typeName
      | (typeName, Some) -> Generic
        {
          Qualified
          {
            Simple { Name = Reference(this.Location, "System"); } -> Qualifier;
            Name = Reference(this.Location, "Nullable");
          } -> QualifiedName;
          [ typeName ] -> Arguments;
        }
    }
  }

  map syntax Unsafe.TypeSpecifier.PointerSpecifier(qualifiedName : QualifiedReference) -> QualifiedReference
  {
    Generic
    {
      Simple { Name = Reference(this.Location, "#Pointer"); } -> QualifiedName;
      [qualifiedName] -> Arguments;
    }
  }

  map syntax Types.TypeSpecifier(qualifiedName : QualifiedReference) -> QualifiedReference
  {
    | RankSpecifier -> Generic
      {
        Simple { Name = Reference(this.Location, "#Array" + (RankSpecifier.RankSeparators.Count + 1).ToString()); } -> QualifiedName;
        [qualifiedName] -> Arguments;
      }
  }

  map syntax Types.AnyType -> QualifiedReference
  {
    fold (ts in TypeSpecifiers; acc = AnyNonArrayType; acc = ts(acc))
  }

  map syntax Types.AnyTypeNullableHack -> QualifiedReference
  {
    fold (ts in TypeSpecifiers; acc = AnyNonArrayTypeNullableHack; acc = ts(acc))
  }

  map syntax Statements.LocalVariableType -> QualifiedReference
  {
    | Var  -> CsVarReference { Name = Reference(Location(Location.Source, KwVar), "var"); }
    | Type -> this.AnyType
  }

  map syntax Attributes.Attribute(targetOpt : CustomAttributeTarget?) -> CustomAttributeReference
  {
    targetOpt     -> Target;
    QualifiedName -> Name;
  }

  map syntax Attributes.AttributeList(targetOpt : CustomAttributeTarget?) -> CustomAttributeReference*
  {
    Attributes.Item1(targetOpt)
  }

  map syntax Attributes.AttributeSection -> CustomAttributeReference*
  {
    AttributeList(AttributeTargetSpecifierOpt)
  }

  map syntax Attributes.GlobalAttributeSection -> CustomAttributeReference*
  {
    AttributeList(Some(GlobalAttributeTargetSpecifier))
  }

  map syntax Attributes.AttributeTargetSpecifier -> CustomAttributeTarget =
  {
    match (this.GetText(this.AttributeTarget))
    {
      | "field"    => CustomAttributeTarget.Field(this.Location.WithSpan(this.AttributeTarget))
      | "event"    => CustomAttributeTarget.Event(this.Location.WithSpan(this.AttributeTarget))
      | "method"   => CustomAttributeTarget.Method(this.Location.WithSpan(this.AttributeTarget))
      | "param"    => CustomAttributeTarget.Param(this.Location.WithSpan(this.AttributeTarget))
      | "property" => CustomAttributeTarget.Property(this.Location.WithSpan(this.AttributeTarget))
      | "return"   => CustomAttributeTarget.Return(this.Location.WithSpan(this.AttributeTarget))
      | "type"     => CustomAttributeTarget.Type(this.Location.WithSpan(this.AttributeTarget))
      | "typevar"  => CustomAttributeTarget.Typevar(this.Location.WithSpan(this.AttributeTarget))
      | _          => CustomAttributeTarget.AmbiguousOrMissing(this.Location.WithSpan(this.AttributeTarget))
    }
  }

  map syntax Attributes.GlobalAttributeTargetSpecifier -> CustomAttributeTarget =
  {
    match (this.GetText(this.GlobalAttributeTarget))
    {
      | "assembly" => CustomAttributeTarget.Assembly(this.Location.WithSpan(this.GlobalAttributeTarget))
      | "module"   => CustomAttributeTarget.Module(this.Location.WithSpan(this.GlobalAttributeTarget))
      | _          => CustomAttributeTarget.AmbiguousOrMissing(this.Location.WithSpan(this.GlobalAttributeTarget))
    }
  }

  map syntax Literals.VerbatimStringLiteral -> StringLiteral.Regula
  {
    Parts -> Parts;
  }

  map syntax Literals.RegularStringLiteral -> StringLiteral.Verbatim
  {
    Parts -> Parts;
  }

  map syntax Literals.RegularStringLiteralPart -> StringLiteralPart
  {
    | SimpleEscapeSequenceStrPart           -> SimpleEscape      { }
    | UnicodeEscapeSequenceStrPart          -> UnicodeEscape     { }
    | HexadecimalEscapeSequenceStrPart      -> HexadecimalEscape { }
    | RegularStringLiteralCharacterSequence -> RegulaChars       { }
  }

  map syntax Literals.VerbatimStringLiteralPart -> StringLiteralPart
  {
    | QuoteEscapeSequence                    -> VerbatimEscape { }
    | VerbatimStringLiteralCharacterSequence -> RegulaChars    { }
  }

  map syntax CS6Literals.InterpolatedRegularStringLiteral -> StringLiteral.InterpolatedRegula
  {
    Parts -> Parts;
  }

  map syntax CS6Literals.InterpolatedVerbatimStringLiteral -> StringLiteral.InterpolatedVerbatim
  {
    Parts -> Parts;
  }

  map syntax CS6Literals.InterpolatedRegularStringLiteralPart -> StringLiteralPart
  {
      | Interpolation1                        -> Interpolation           { Expr -> Expr; }
      | Interpolation2                        -> InterpolationFormated   { Expr -> Expr; Format -> Format; }
      | OpenBraceEscapeSequence               -> InterpolatedOpenEscape  { }
      | CloseBraceEscapeSequence              -> InterpolatedCloseEscape { }
      | ErrorOpenBrace                        -> Error                   { }
      | ErrorCloseBrace                       -> Error                   { }
      | SimpleEscapeSequenceStrPart           -> SimpleEscape            { }
      | UnicodeEscapeSequenceStrPart          -> UnicodeEscape           { }
      | HexadecimalEscapeSequenceStrPart      -> HexadecimalEscape       { }
      | RegularStringLiteralCharacterSequence -> RegulaChars             { }
  }

  map syntax CS6Literals.InterpolatedVerbatimStringLiteralPart -> StringLiteralPart
  {
      | Interpolation1                         -> Interpolation           { Expr -> Expr; }
      | Interpolation2                         -> InterpolationFormated   { Expr -> Expr; Format -> Format; }
      | OpenBraceEscapeSequence                -> InterpolatedOpenEscape  { }
      | CloseBraceEscapeSequence               -> InterpolatedCloseEscape { }
      | ErrorOpenBrace                         -> Error                   { }
      | ErrorCloseBrace                        -> Error                   { }
      | QuoteEscapeSequence                    -> VerbatimEscape          { }
      | VerbatimStringLiteralCharacterSequence -> RegulaChars             { }
  }
}
