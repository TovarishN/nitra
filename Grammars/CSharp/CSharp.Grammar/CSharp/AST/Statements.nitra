using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using System;
using System.Globalization;
using System.Collections.Generic;
using System.Collections.Immutable;
using System.Linq;
using Nitra;
using Nitra.Declarations;
using DotNet;

namespace DotNet
{
  ast EmptyStatement : Statement
  {
  }

  ast BlockStatement : Statement
  {
    LocalVarsScopeIn = null;

    Statements.LocalVarsScopeIn = LocalVarsScopeIn;
    Statements.ParameterScope   = ParameterScope;
    Statements.ScopeIn          = ScopeIn;

    Statements : Statement*;
  }

  ast LocalVarsStatement : Statement
  {
    LocalVars.ContainingTable = LocalVarsScopeOut;
    LocalVars.Scope           = ScopeIn;
    LocalVarsScopeOut         = TableScope("LocalVarsScope");
    ScopeOut                  = ScopeIn.HideWith(LocalVarsScopeOut);

    LocalVars : LocalVar*;
  }

  declaration LocalVar : TypedDeclaration
  {
    symbol
    {
      Kind  = "variable";
    }
    in Scope : Scope;
    in TypeRef : Ref[TypeSymbol];

    Type.Scope = Scope;
    Expression.ScopeIn = Scope;
    // The type of a local variable can be shared between two declarations.
    // Use ResolveOrCopy() method to avoid multiple type resolutions.
    TypeRef = Type.Ref.ResolveOrCopy();
    Expression.ExpectedType = TypeRef.Symbol;
    Symbol.Scope = TypeRef.Scope;
    Symbol.Type  = TypeRef.Symbol;

    Type : QualifiedReference;
    Expression : Expr;
  }

  ast TupleDeconstruction : Statement
  {
    // TODO: Implement...
  }

  ast ExpressionStatement : Statement
  {
    Expr.ScopeIn = ScopeIn;
    Expr.ExpectedType = Expr.Type; // В C# допускается игнорировать возвращаемые значения. После типизации такие случаи можно превратить в ворнинги.

    Expr : Expr;
  }
}
